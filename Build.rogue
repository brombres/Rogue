# To run this build file, install Rogue from github.com/brombres/Rogue then cd
# to this folder and type "rogo" at the command line, or "rogo help" for a list
# of supported commands.

$requireRogue "2.34"

augment Build
  # You can create a Local.settings file with overrides for these values, e.g.:
  # BUILD_MODE = release
  PROPERTIES
    PROJECT          = "RogueC"
    LAUNCHER_NAME    = PROJECT.to_lowercase : String

    BUILD_MODE       = "release"
    LAUNCHER_FOLDER  = "/usr/local/bin"
    ROGUEC_FLAGS     = "--main --gc=manual"
    CC_FLAGS_UNIX    = ""
    CC_FLAGS_WINDOWS = "/bigobj"
endAugment

if (System.is_windows and File("Build/roguec.exe.old").exists)
  File("Build/roguec.exe.old").delete
endIf

routine rogo_bootstrap
  local files = Files("Source/RogueC/Bootstrap/RogueC-*.c")
  local c_files = files->String[].map<<String>>( File($1).esc ).join(' ')
  execute "cc -O3 -Wall -fno-strict-aliasing $ -o Build/RogueC-macOS -lm"(c_files)
endRoutine

routine rogo_bootstrap_debug
endRoutine

routine rogo_update_bootstrap
  rogo_build

  local existing = Files("Source/RogueC/Bootstrap")
  existing.add( "*.h" )
  existing.add( "*.c" )
  existing.delete( &verbose )

  copy_bootstrap_file( "h" )
  copy_bootstrap_file( "c" )
endRoutine

routine copy_bootstrap_file( ext:String )
  forEach (filepath in Files("Build/RogueC-$*.$"(System.os,ext)))
    println "> copy $ Source/RogueC/Bootstrap/$"(filepath,File(filepath).filename)
    local content = String(File(filepath))
    content .= replacing( "RogueC-$"(System.os), "RogueC" )
    if (ext == "h" )
      File("Source/RogueC/Bootstrap/RogueC.$"(ext)).save( content )
    else
      local dest_filepath = "Source/RogueC/Bootstrap"/File(filepath).filename
      dest_filepath .= replacing( "RogueC-$"(System.os), "RogueC" )
      File(dest_filepath).save( content )
    endIf
  endForEach
endRoutine


routine rogo_default
  # Builds and runs this project - equivalent to 'rogo build && rogo run'.
  rogo_build
  rogo_run
endRoutine

routine rogo_run
  # Runs this project.
  execute "$ --version"(exe_file.esc)
endRoutine

routine rogo_debug
  # Compiles in debug mode and runs.
  rogo_build_debug
  rogo_run
endRoutine

routine rogo_release
  # Compiles in release mode and runs.
  rogo_build_release
  rogo_run
endRoutine

routine rogo_clean
  # Deletes the Build/ and .rogo/ folders.
  verbose_delete( "Build" )
  verbose_delete( ".rogo" )
endRoutine

routine rogo_build
  # Recompiles Rogue -> C -> Exe if needed. 'rogo rebuild' forces a build.
  rogo_libs

  localize exe_file

  if (File("Source/RogueC/Rogue.froley").is_newer_than(exe_file))
    rogo_froley
  endIf

  local source_files = Files( "Source/RogueC", "**/*.rogue" )
  if (source_files.any_newer_than(exe_file))
    local h_filepath = "Build/$-$.h" (Build.PROJECT,System.os)
    if (source_files.any_newer_than(h_filepath))
      rogo_compile_rogue
    endIf
    rogo_compile_c
  endIf
endRoutine

routine rogo_libs
  Files("Source/Libraries/**").sync_to(File("Build/Libraries"),&verbose)
endRoutine

routine rogo_froley
  local froley_src = File( "Source/RogueC/Rogue.froley" )
  execute "froley --project=RogueC $ Source/$"(froley_src.esc,Build.PROJECT)
endRoutine

routine rogo_build_debug( &call_stack )
  # Force-rebuilds in debug mode.
  rogo_rebuild_debug( &=call_stack )
endRoutine

routine rogo_build_release
  # Force-rebuilds in release mode.
  rogo_rebuild_release
endRoutine

routine rogo_rebuild( &split )
  # Force-rebuilds project.
  rogo_froley
  rogo_compile_rogue( &=split )
  rogo_compile_c
endRoutine

routine rogo_rebuild_debug( &call_stack )
  # Force-rebuilds in debug mode.
  Build.BUILD_MODE = "debug"
  if (call_stack) Build.ROGUEC_FLAGS .= appending( "--debug" )
  rogo_rebuild
endRoutine

routine rogo_rebuild_release
  # Force-rebuilds in release mode.
  Build.BUILD_MODE = "release"
  rogo_rebuild
endRoutine

routine rogo_update_wiki
  if (not File("Wiki").exists)
    execute @|git clone git@github.com:brombres/Rogue.wiki.git Wiki
  endIf

  execute @|cd Wiki && git pull
  execute @|roguec --doc --output=.rogo/API.json
  execute @|roguedoc .rogo/API.json --output=Wiki --split

  execute @|cd Wiki && git add . && git commit -am "[API]"
  local branch = Process.run( "cd Wiki && git symbolic-ref --short HEAD", &env )->String.trimmed
  execute "cd Wiki && git push origin $"(branch)
endRoutine

routine rogo_install
  # Builds and installs the Exe to a system-appropriate location (see also 'rogo link').
  rogo_build
  local exe_filepath = exe_file.abs.filepath
  if (System.is_windows)
    local exe_folder = File(exe_filepath).parent.conventional_filepath
    local path = System.env//PATH
    if (not path.to_lowercase.contains(exe_folder.to_lowercase))
      println
      println ''Add the following to your system PATH then reopen this command prompt to be able''
      println ''to launch $ by typing "$":'' (Build.PROJECT,Build.LAUNCHER_NAME)
      println
      println "  ADD THIS TO YOUR PATH"
      println "  " + exe_folder
    endIf
  else
    contingent
      local result = Process.run( "which " + Build.LAUNCHER_NAME, &env )
      necessary (result.success)
      local launcher_filepath = result->String.trimmed
      necessary launcher_filepath
      necessary String( File(launcher_filepath) ).contains( exe_filepath )

    unsatisfied
      local launcher_filepath = "$/$" (launcher_folder,Build.LAUNCHER_NAME)
      println "Creating launcher " + launcher_filepath
      local sudo = ""
      loop
        try
          if (not File(launcher_folder).exists) execute( "$mkdir $"(sudo,launcher_folder) )
          File( "$.launcher"(Build.LAUNCHER_NAME) ).save( ''#!/bin/sh\nexec "$" "$@"\n''(exe_filepath,'$') )
          execute( "$mv $.launcher $"(sudo,Build.LAUNCHER_NAME,launcher_filepath) )
          execute( "$chmod a+x $"(sudo,launcher_filepath) )
          escapeLoop
        catch (err:Error)
          if (sudo != "") throw err
          sudo = "sudo "
        endTry
      endLoop
    endContingent
  endIf
endRoutine

routine rogo_link
  # Builds and invokes 'morlock link ...' to add a launcher to Morlock's binpath.
  rogo_build
  execute "morlock link $ $"(Build.LAUNCHER_NAME,exe_file.esc)
endRoutine

routine rogo_uninstall
  # Cleans the project and uninstalls the launcher from 'rogo install'.
  rogo_clean
  if (not System.is_windows) verbose_delete( "$/$"(launcher_folder,Build.LAUNCHER_NAME) )
endRoutine

routine rogo_update_version( version:String )
  # Updates the version number and date in the project source and README.md.
  if (version.count == 0) throw Error( "New version number expected, e.g. '1.0.2'." )

  block file = File( "Source/RogueC/RogueC.rogue" )
    if (file.exists)
      local og_content = String(file)
      local content = og_content

      content = content.replacing_pattern(
      ''define VERSION "$"'',
      ''define VERSION "$"''(version)
      )

      content = content.replacing_pattern(
        ''define DATE "$"'',
        ''define DATE    "$"''(Date.today->String(&verbose))
      )

      if (content != og_content)
        println "Updating version and date in $"(file)
        file.save( content )
      endIf
    endIf
  endBlock

  block file = File( "README.md" )
    if (file.exists)
      local og_content = String(file)
      local content = String( og_content.count+20 )
      forEach (line in LineReader(og_content))
        if (line.contains_pattern("Version|$(I)"))
          content.println( "$| $" (line.before_last('|'),version) )
        elseIf (line.contains_pattern("Date|"))
          local today = Date.today->String(&verbose)
          content.println( "$| $" (line.before_last('|'),today) )
        else
          content.println( line )
        endIf
      endForEach
      if (content != og_content)
        println "Updating version and date in $"(file)
        file.save( content )
      endIf
    endIf
  endBlock
endRoutine

routine rogo_commit( new_version:String )
  # Updates source and README version number and git-commits with a version number message.
  if (not String.exists(new_version)) throw Error( "Expected version number after 'rogo commit'." )
  rogo_update_version( new_version )

  if (not System.find_executable("git")) throw Error( "Git must be installed." )
  if (project_has_uncommitted_changes)
    execute ''git commit -am "[v$]"'' (new_version)
  else
    println "No changes to commit."
  endIf
endRoutine

routine project_has_uncommitted_changes->Logical
  local result = Process.run( "git status --porcelain", &env )
  if (not result.success)
    Console.error.println result->String
    System.exit 1
  endIf

  local lines = result->String.trimmed.split('\n').[discard($.begins_with("??"))]
  lines.discard( $ == "" )
  return not lines.is_empty
endRoutine

routine rogo_publish( new_version:String )
  # Updates source and README version number, commits, and publishes a release to GitHub.
  if (not String.exists(new_version)) throw Error( "Expected version number after 'rogo publish'." )
  if (not System.find_executable("gh")) throw Error( "The GitHub command line tool 'gh' must be installed." )

  rogo_update_version( new_version )

  rogo_build
  rogo_update_bootstrap

  rogo_commit( new_version )

  # These can be hard-coded to suit your project
  local cur_branch : String
  local main_branch : String

  if (not cur_branch)
    local result = Process.run("git branch --show-current",&env)
    if (result.success) cur_branch = result->String.trimmed
    else                throw Error( "Unable to detect current git branch." )
  endIf

  if (not main_branch)
    local result = Process.run("git branch --list",&env)
    if (result.success)
      local branches = result->String.replacing('*',' ').split('\n').[modify($.trimmed)]
      forEach (branch in ["main","master"])
        if (branches.contains(branch)) main_branch=branch; escapeForEach
      endForEach
    endIf
    if (not main_branch) throw Error( "Unable to detect name of main (release) branch." )
  endIf

  if (cur_branch != main_branch)
    if (not Console.input("$ will be merged into $. Continue? "(cur_branch,main_branch)).to_lowercase.begins_with('y')) return
    execute "git push origin $"(cur_branch)
    execute "git checkout $"(main_branch)
    execute "git pull"
    execute "git merge $"(cur_branch)
    execute "git push origin $"(main_branch)
    execute "git checkout $"(cur_branch)
  endIf

  if (not Console.input("Continue publishing release to GitHub? ").to_lowercase.begins_with('y')) return

  execute ''gh release create v$ --title "v$" --notes ""''(new_version,new_version)
  execute "git pull"

  rogo_update_wiki
endRoutine

routine rogo_compile_rogue( &split )
  rogo_libs
  local src_filepath = "Source/RogueC/$.rogue" (Build.PROJECT)
  local c_filename   = "$-$" (Build.PROJECT,System.os)
  local c_filepath   = "Build"/c_filename

  local existing = Files( "Build" )
  existing.add( "$.h"(c_filename) )
  existing.add( "$*.c"(c_filename) )
  existing.delete

  local flags = Build.ROGUEC_FLAGS
  if (Build.BUILD_MODE == "debug") flags .= appending("--debug")
  if (split) flags .= appending( "--split" )
  local cmd = "roguec $ $ --target=Console,C,$ --output=$"...
              (File(src_filepath).esc,flags,System.os,File(c_filepath).esc)
  println "> " + cmd
  local error = System.run( cmd )
  if (error) throw Error( "Build failed." )
endRoutine

routine rogo_compile_c
  local c_filename   = "$-$" (Build.PROJECT,System.os)
  local c_files = Files( "Build/$*.c"(c_filename) )->String[].map<<String>>( File($1).esc ).join(' ')

  local exe_filepath = exe_file.esc
  if (System.is_windows)
    local flags = Build.CC_FLAGS_WINDOWS
    if (flags != "") flags += " "
    flags += which{ Build.BUILD_MODE=="release":"/O2 /bigobj" || "/Od" }
    local obj_filepath = File( "Build/$-Windows.obj"(Build.PROJECT) ).esc
    local cc = "cl $ /nologo $.c /Fo$ /Fe$.new" (flags,c_files,obj_filepath,exe_filepath)
    cc .= appending( "$LIBRARY_FLAGS" )

    execute cc

    File(exe_filepath).rename( File(exe_filepath+".old") )
    File(exe_filepath+".new").rename( File(exe_filepath) )
  else
    local flags = Build.CC_FLAGS_UNIX
    if (flags != "") flags += " "
    flags += which{ Build.BUILD_MODE=="release":"-O3" || "-O0" }
    local cc = "cc $ -Wall -fno-strict-aliasing $ -o $ -lm" (flags,c_files,exe_file.esc)
    cc .= appending( "$LIBRARY_FLAGS" )
    execute cc
  endIf
endRoutine

routine exe_file->File
  if (System.is_windows) return File( "Build/$.exe"(Build.LAUNCHER_NAME) )
  return File( "Build/$-$"(Build.PROJECT,System.os) )
endRoutine

routine launcher_folder->String
  local result = Build.LAUNCHER_FOLDER
  result = File(result).resolved.filepath
  if (result.count > 1) result .= without_suffix('/')
  return result
endRoutine

routine verbose_delete( filepath:String )
  if (File(filepath).exists)
    println "> Delete " + filepath
    if (not File(filepath).delete)
      println "*** Failed to delete - retrying with sudo"
      local cmd = ''sudo rm -rf $'' (File(filepath).esc)
      execute cmd
    endIf
  endIf
endRoutine

routine execute( commands:String, error_message=null:String, &suppress_error )->Logical
  forEach (cmd in LineReader(commands))
    ConsoleStyle.set<<FG_LIGHT_GREEN>>
    print( "> " )
    println( cmd )
    ConsoleStyle.set<<FG_DEFAULT>>
    if (0 != System.run(cmd))
      if (suppress_error)
        return false
      else
        if (not error_message) error_message = "Build failed."
        throw Error( error_message )
      endIf
    endIf
  endForEach
  return true
endRoutine

#-------------------------------------------------------------------------------
# Introspection-based Launcher Framework
#-------------------------------------------------------------------------------
# Rogo is a "build your own build system" facilitator. At its core Rogo just
# recompiles build files if needed and then runs the build executable while
# forwarding any command line arguments. This file contains a default framework
# which uses introspection to turn command line arguments into parameterized
# routine calls.

# Example: to handle the command "rogo abc xyz 5", define
# "routine rogo_abc_xyz( n:Int32 )".

# "rogo_default" will run in the absence of any other command line argument.

# The following "comment directives" can be used in this file to control how
# RogueC compiles it and to manage automatic dependency installation and
# linking.

# Each of the following should be on a line beginning with the characters #$
# (preceding whitespace is fine). Sample args are given.

#   ROGUEC       = roguec       # Path to roguec to compile this file with
#   ROGUEC_ARGS  = --whatever   # Additional options to pass to RogueC
#   CC           = gcc -Wall -fno-strict-aliasing
#   CC_ARGS      = -a -b -c          # Additional C args
#   LINK         = -lalpha -lbeta    # Link this build file with these options
#   LINK(macOS)  = ...               # Options applying only to
#                                    # System.os=="macOS" (use with any OS and
#                                    # any comment directive)
#   LINK_LIBS    = true              # Links following LIBRARIES with this Build
#                                    # file (otherwise just installs them)
#   LINK_LIBS    = false             # Linking turned off for following
#                                    # LIBRARIES - info can still be obtained
#                                    # from $LIBRARY_FLAGS or $LIBRARIES(libname,...)
#   LIBRARIES    = libalpha
#   LIBRARIES    = libbeta(library-name)
#   LIBRARIES    = libfreetype6-dev(freetype2)
#   DEPENDENCIES = Library/Rogue/**/*.rogue
#
#   LIBRARIES    = name(package)
#   LIBRARIES    = name(package:<package> install:<install-cmd>
#                  link:<link-flags> which:<which-name>)
#
# The following macro is replaced within this file (Build.rogue) - the libraries
# should normally also be declared in #$ LIBRARIES:
#
#   $LIBRARY_FLAGS(lib1,lib2)                              # sample macro
#     ->
#   -Ipath/to/lib1/include -Lpath/to/lib1/library -I ...   # sample replacement

routine syntax( command:String, text:String )
  Build.rogo_syntax[ command ] = text
endRoutine

routine description( command:String, text:String )
  Build.rogo_descriptions[ command ] = text
endRoutine

routine help( command:String, description_text=null:String, syntax_text=null:String )
  if (description_text) description( command, description_text )
  if (syntax_text)      syntax( command, syntax_text )
endRoutine

try
  Build.launch
catch (err:Error)
  Build.rogo_error = err
  Build.on_error
endTry

class Build [singleton]
  PROPERTIES
    rogo_syntax         = [String:String]
    rogo_descriptions   = [String:String]
    rogo_prefix         = "rogo_"
    rogo_command        = "default"
    rogo_args           = @[]
    rogo_error          : Error

    LOCAL_SETTINGS_FILE = "Local.settings"

  METHODS
    method launch
      rogo_args.add( forEach in System.command_line_arguments )
      read_defs
      on_launch
      parse_args
      dispatch_command

    method dispatch_command
      local m = find_command( rogo_command )
      if (not m) throw Error( "No such routine rogo_$()" (rogo_command) )

      local args = @[]
      forEach (arg in rogo_args)
        which (arg)
          case "true":  args.add( true )
          case "false": args.add( false )
          case "null":  args.add( null )
          others:       args.add( arg )
        endWhich
      endForEach
      m( args )

    method find_command( name:String )->MethodInfo
      return <<Routine>>.find_global_method( rogo_prefix + name )

    method on_error
      local w = Console.width.or_smaller( 80 )
      Console.error.println "=" * w
      Console.error.println rogo_error
      Console.error.println "=" * w
      on_exit
      System.exit 1

    method on_command_found
      noAction

    method on_command_not_found
      local w = Console.width.or_smaller( 80 )
      println "=" * w
      println "ERROR: No such command '$'." (rogo_args.first)
      println "=" * w
      println
      rogo_command = "help"
      rogo_args.clear
      on_command_found

    method on_launch
      noAction

    method on_exit
      noAction

    method parse_args
      block
        if (rogo_args.count)
          local parts = String[]
          parts.add( forEach in rogo_args )
          rogo_args.clear

          while (parts.count)
            local cmd = parts.join("_")
            if (find_command(cmd))
              rogo_command = cmd
              on_command_found
              escapeBlock
            endIf
            rogo_args.insert( parts.remove_last )
          endWhile

          on_command_not_found
        endIf

        # Use default command
        on_command_found
      endBlock

    method read_defs
      read_defs( LOCAL_SETTINGS_FILE )

    method read_defs( defs_filepath:String )
      # Attempt to read defs from Local.settings
      local overrides = String[]
      if (File(defs_filepath).exists)
        forEach (line in LineReader(File(defs_filepath)))
          if (line.contains("="))
            local name  = line.before_first('=').trimmed
            local value = line.after_first('=').trimmed
            if (value.begins_with('"') or value.begins_with('\''))
              value = value.leftmost(-1).rightmost(-1)
            endIf
            local p = <<Build>>.find_property( name )
            if (p)
              overrides.add( "$ = $" (name,value) )
              p.set_value( this, value )
            endIf
          endIf
        endForEach
      endIf

    method _join( value:Variant )->String
      local args = String[]
      args.add( forEach in value )
      return args.join( "_" )
endClass


routine rogo_help( command="":String )
  # SYNTAX: rogo help [command]
  # Displays help for a specified command or else all build commands.
  command = Build._join( Build.rogo_args )
  if (command.count)
    local syntax = get_syntax( command )
    local success = false
    if (syntax)
      println "SYNTAX"
      println "  " + syntax
      println
      success = true
    endIf
    local description = get_description( command )
    if (description)
      local max_w = Console.width - 2
      println "DESCRIPTION"
      forEach (line in LineReader(description.word_wrapped(max_w)))
        print( "  " )
        println( line )
      endForEach
      println
      success = true
    endIf
    if (success)
      return
    else
      local w = Console.width.or_smaller( 80 )
      println "=" * w
      println "ERROR: No such command '$'." (command)
      println "=" * w
      println
    endIf
  endIf

  println "USAGE"
  local entries = CommandInfo[]
  local max_len = 0
  forEach (m in <<Routine>>.global_methods)
    if (m.name.begins_with(Build.rogo_prefix))
      local name = m.name.after_first( Build.rogo_prefix )
      local entry = CommandInfo( name, get_syntax(name), get_description(name) )
      max_len .= or_larger( entry.syntax.count )
      entries.add entry
    endIf
  endForEach

  entries.sort( $1.name < $2.name )
  max_len += 2

  local max_w = Console.width
  forEach (entry in entries)
    print "  " + entry.syntax
    if (entry.@description)
      local description = entry.@description
      loop (max_len - entry.syntax.count) print ' '
      contingent
        sufficient (max_len + description.count <= max_w)
        if (description.contains(". "))
          description = description.before_first( ". " ) + "."
          sufficient (max_len + description.count <= max_w)
        endIf
        necessary (max_len + 10 <= max_w)
        description = description.unright( (description.count - (max_w - max_len))+3 ) + "..."
      satisfied
        print description
      endContingent
    endIf
    println
  endForEach
  println
endRoutine

routine get_syntax( m_name:String )->String
  if (Build.rogo_syntax.contains(m_name))
    return "rogo " + Build.rogo_syntax[ m_name ]
  else
    local m = <<Routine>>.find_global_method( Build.rogo_prefix + m_name )
    if (not m) return null
    local line = "rogo $" (m_name.replacing('_',' '))
    line += " <$>" ((forEach in m.parameters).name)
    return line
  endIf
endRoutine


routine get_description( m_name:String )->String
  if (Build.rogo_descriptions.contains(m_name))
    return Build.rogo_descriptions[ m_name ]
  else
    return null
  endIf
endRoutine

class CommandInfo( name:String, syntax:String, description:String );
