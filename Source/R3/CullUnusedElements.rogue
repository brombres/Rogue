module RogueC

class CullUnusedElements : InspectionVisitor
  PROPERTIES
    is_modified = true

  METHODS
    method apply
      ResolveUsedElements().apply

      # Cull unused types and methods
      forEach (mod in Program.modules)
        use unused_type_names = WorkList<<String>>
          forEach (type in mod.types)
            if (not type.attributes.is_used)
              unused_type_names.add( type.name )
            endIf
          endForEach
          mod.types.remove( forEach in unused_type_names )
        endUse

        forEach (type in mod.types)
          use unused_global_method_sigs = WorkList<<String>>
            forEach (m in type.global_methods)
              if (not m.attributes.is_used)
                unused_global_method_sigs.add( m.signature )
              endIf
            endForEach
            type.global_methods.remove( forEach in unused_global_method_sigs )
          endUse

          use unused_method_sigs = WorkList<<String>>
            forEach (m in type.methods)
              if (not m.attributes.is_used)
                unused_method_sigs.add( m.signature )
              endIf
            endForEach
            type.methods.remove( forEach in unused_method_sigs )
          endUse
        endForEach
      endForEach

      forEach (group in Program.call_signatures.values)
        group.remove( not $.attributes.is_used or $.type_context.is_aspect )
      endForEach

endClass
