uses Geometry [export]
uses Utility/Epilog

class TextCanvas  : BufferedPrintWriter<<builder>>
  # A 2D canvas of styled console text.
  PROPERTIES
    buffer       : StyledCharacter[]
    displayed    = StyledCharacter[]
    width        : Int
    height       : Int

    cursor       : XY
    autoscroll   : Logical
    style        = CurrentCharacterStyle()

    clip         : Box?

    _is_flushing : Logical

  METHODS
    method init( width, height, fill=' ':Character )
      init( width, height, StyledCharacter(fill,style.current) )

    method init( width, height, fill:StyledCharacter )
      buffer = StyledCharacter[]( width * height )
      loop (width*height) buffer.add( fill )
      invalidate

    method init( width, height, content:String )
      init( width, height, ' ' )
      if (content.contains('\n'))
        local y = 0
        forEach (line in LineReader(content))
          forEach (ch at x in line)
            set( x, y, ch )
          endForEach
          ++y
        endForEach
      else
        buffer.clear
        buffer.add( StyledCharacter(forEach in content) )
      endIf

    method init( width, height, buffer )
      invalidate

    method init( content:String, width:Int )
      init( width, content.count/width )
      draw( content, width )

    method init( lines:String[] )
      local max_w = 0
      max_w .= or_larger( (forEach in lines).count )
      lines = lines.map<<String>>( (line) with (max_w) => line.justified(-max_w) )
      init( lines.join(""), max_w )

    method cloned->TextCanvas
      return TextCanvas( width, height, buffer.cloned ).[ cursor=cursor ]

    method clear( fill:StyledCharacter )
      forEach (i of buffer) buffer[i] = fill
      cursor = XY(0,0)

    method clear( fill=' ':Character )
      clear( StyledCharacter(fill,style.current) )

    method crop( n:Int )
      crop( n, n, n, n )

    method crop( left:Int, top:Int, right:Int, bottom:Int )
      expand( -left, -top, -right, -bottom )

    method display( output=Global:PrintWriter )
      # Prints to the standard output or a specified PrintWriter.
      flush

      ConsoleStyle.reset( output )

      local cur_style = CharacterStyle.NONE
      local index = 0
      loop height
        loop width
          local ch = buffer[ index ]
          ++index
          cur_style = cur_style.apply( ch.style, output )
          output.print( ch.value )
        endLoop

        # On Windows must turn off style before println or else it extends across
        cur_style = CharacterStyle.NONE
        ConsoleStyle.reset( output )
        output.println
      endLoop

      ConsoleStyle.reset( output )
      style.reset

    method display( x:Int, y:Int )
      # (0,0) is the top-left corner of the console
      flush
      ConsoleStyle.reset

      local cur_style = CharacterStyle.NONE
      local line_start = 0
      forEach (j in 0..<height)
        local is_drawing = false
        forEach (i in 0..<width)
          local index = line_start + i
          local ch = buffer[index]
          if (ch == displayed[index])
            is_drawing = false
          else
            if (not is_drawing)
              is_drawing = true
              Console.set_cursor( x+i, y+j )
            endIf
            cur_style = cur_style.apply( ch.style )
            Console.print( ch.value )
            displayed[index] = ch
          endIf
        endForEach
        line_start += width
      endForEach

      ConsoleStyle.reset
      style.reset

    method display( xy:XY )
      display( xy.x, xy.y )

    method draw( src:TextCanvas, x:Int, y:Int )
      local i1 = 0
      local j1 = 0
      local i2 = src.width-1
      local j2 = src.height-1
      local x2 = x + i2
      local y2 = y + j2

      local min_x = which{ clip:clip.value.position.x || 0 }
      local min_y = which{ clip:clip.value.position.y || 0 }
      local max_x = which{ clip:clip.value.x2-1 || width-1 }
      local max_y = which{ clip:clip.value.y2-1 || height-1 }

      if (x < min_x) i1 += min_x - x; x = min_x
      if (y < min_y) j1 += min_y - y; y = min_y
      if (x2 > max_x)
        local excess = (x2 - max_x)
        x2 -= excess
        i2 -= excess
      endIf
      if (y2 > max_y)
        local excess = (y2 - max_y)
        y2 -= excess
        j2 -= excess
      endIf

      if (i1 > i2 or j1 > j2) return  # completely OOB

      local src_width = src.width
      local dest_width = width
      local src_i1  = (j1 * src_width) + i1
      local dest_i1 = (y * dest_width) + x
      local src_data = src.buffer
      forEach (j1..j2)
        local src_i = src_i1
        local dest_i = dest_i1
        forEach (i1..i2)
          buffer[dest_i] = src_data[src_i]
          ++src_i
          ++dest_i
        endForEach
        src_i1 += src_width
        dest_i1 += dest_width
      endForEach

    method draw( src:TextCanvas, xy:XY )
      draw( src, xy.x, xy.y )

    method draw( bounds:Box, ch:Character )
      draw( bounds, StyledCharacter(ch,style.current) )

    method draw( bounds:Box, ch:StyledCharacter )
      local w = bounds.width
      local h = bounds.height
      local x1 = bounds.x    : Int
      local y1 = bounds.y    : Int
      local x2 = bounds.x2-1 : Int
      local y2 = bounds.y2-1 : Int
      fill( Box(x1,y1,w,1), ch )
      fill( Box(x1,y2,w,1), ch )
      fill( Box(x1,y1+1,1,h-2), ch )
      fill( Box(x2,y1+1,1,h-2), ch )

    method draw( bounds:Box, pattern:String )
      # Pattern: "ABCDEFGHI" where:
      #   ABC
      #   D F
      #   GHI
      # Example:
      #   ABBBBC
      #   D    F
      #   D    F
      #   GHHHHI
      local w = bounds.width
      local h = bounds.height
      local x1 = bounds.x    : Int
      local y1 = bounds.y    : Int
      local x2 = (bounds.x2-1).clamped_low(x1) : Int
      local y2 = (bounds.y2-1).clamped_low(y1) : Int
      fill( Box(x1+1,y1,(w-2).clamped_low(0),1), pattern[1%pattern.count] )
      fill( Box(x1+1,y2,(w-2).clamped_low(0),1), pattern[7%pattern.count] )
      fill( Box(x1,y1+1,1,(h-2).clamped_low(0)), pattern[3%pattern.count] )
      fill( Box(x2,y1+1,1,(h-2).clamped_low(0)), pattern[5%pattern.count] )
      set( x1, y1, pattern[0] )
      set( x2, y1, pattern[2%pattern.count] )
      set( x1, y2, pattern[6%pattern.count] )
      set( x2, y2, pattern[8%pattern.count] )

    method draw( text_image:String, width:Int, position=XY.zero:XY )
      local height = text_image.count / width
      local src = 0
      forEach (j in 0..<height)
        forEach (i in 0..<width)
          set( position+XY(i,j), text_image[src] )
          ++src
        endForEach
      endForEach

    method draw( characters:StyledCharacter[], width:Int, position=XY.zero:XY )
      local height = characters.count / width
      local src = 0
      forEach (j in 0..<height)
        forEach (i in 0..<width)
          set( position+XY(i,j), characters[src] )
          ++src
        endForEach
      endForEach

    method draw( pattern:String )
      draw( Box(width,height), pattern )

    method draw_frame( bounds:Box, &thick )
      # Draws a box-character frame, e.g.:
      # ┌───────┐ ┏━━━━━━┓
      # │Default│ ┃&thick┃
      # │(thin) │ ┗━━━━━━┛
      # └───────┘
      if (thick)
        draw( bounds, "\[250f]\[2501]\[2513]\[2503].\[2503]\[2517]\[2501]\[251b]" )
      else
        draw( bounds, "\[250c]\[2500]\[2510]\[2502].\[2502]\[2514]\[2500]\[2518]" )
      endIf

    method expand( n:Int )
      expand( n, n, n, n )

    method expand( left:Int, top:Int, right:Int, bottom:Int )
      if (left == 0 and top == 0 and right == 0 and bottom == 0) return
      local dest = TextCanvas( width+left+right, height+top+bottom )
      dest.draw( this, left, top )
      buffer = dest.buffer
      width = dest.width
      height = dest.height
      invalidate

    method fill( bounds:Box, ch:Character )
      fill( bounds, StyledCharacter(ch,style.current) )

    method fill( ch:Character )
      fill( Box(width,height), StyledCharacter(ch,style.current) )

    method fill( bounds:Box, ch:StyledCharacter )
      local intersection = bounds & Box(width,height)
      if (not intersection) return
      if (clip) intersection = intersection.value & clip.value
      if (not intersection) return
      bounds = intersection.value

      local index = bounds.y * width + bounds.x
      local w = bounds.width
      local row_skip = width - w
      loop (bounds.height)
        loop (w)
          buffer[index] = ch
          ++index
        endLoop
        index += row_skip
      endLoop

    method fill( ch:StyledCharacter )
      fill( Box(width,height), ch )

    method fill( bounds:Box, pattern:String )
      # Pattern: "ABCDEFGHI" where:
      #   ABC
      #   DEF
      #   GHI
      # Example:
      #   ABBBBC
      #   DEEEEF
      #   DEEEEF
      #   GHHHHI
      fill( bounds.cropped(1), pattern[4%pattern.count] )
      draw( bounds, pattern )

    method fill( pattern:String )
      fill( Box(0,0,width,height), pattern )

    method flipped( &h, &v )->TextCanvas
      if (not v) h = true
      local result = TextCanvas( width, height )

      local iterator = this.buffer.iterator

      if (h)
        if (v)
          forEach (j in result.height-1 downTo 0)
            forEach (i in result.width-1 downTo 0)
              result.set( i, j, iterator.read )
            endForEach
          endForEach
        else
          forEach (j in 0..<result.height)
            forEach (i in result.width-1 downTo 0)
              result.set( i, j, iterator.read )
            endForEach
          endForEach
        endIf
      else
        if (v)
          forEach (j in result.height-1 downTo 0)
            forEach (i in 0..<result.width)
              result.set( i, j, iterator.read )
            endForEach
          endForEach
        else
          forEach (j in 0..<result.height)
            forEach (i in 0..<result.width)
              result.set( i, j, iterator.read )
            endForEach
          endForEach
        endIf
      endIf

      return result

    method flush
      if (_is_flushing) return
      temporarily _is_flushing=true
        flush( builder )
      endTemporarily

    method flush( buffer:String )
      local x1 = cursor.x
      forEach (ch in buffer)
        if (ch == '\r') nextIteration
        if (ch == '\n')
          @cursor = XY(x1,cursor.y+1)
          if (autoscroll and cursor.y > height)
            local delta = (cursor.y - height)
            shift( 0, -delta )
            @cursor -= XY(0,delta)
          endIf
        else
          set( cursor, ch )
          @cursor += XY(1,0)
        endIf
      endForEach
      buffer.clear

    method get( i:Int, j:Int )->StyledCharacter
      if (i < 0 or j < 0 or i >= width or j >= height) return StyledCharacter(0)
      return buffer[ j*width + i ]

    method get( xy:XY )->StyledCharacter
      return get( xy.x, xy.y )

    method invalidate
      displayed.resize( width*height )

      local zero = StyledCharacter(0)
      displayed[ forEach of displayed ] = zero

    method invalidate( bounds:Box )
      local intersection = bounds & Box(width,height)
      if (not intersection) return
      bounds = intersection.value

      local index = bounds.y * width + bounds.x
      local w = bounds.width
      local row_skip = width - w
      loop (bounds.height)
        loop (w)
          displayed[index] = StyledCharacter(0)
          ++index
        endLoop
        index += row_skip
      endLoop

    method pixellated( style=CharacterStyle.NONE:CharacterStyle )->TextCanvas
      # "Pixellates" non-space characters in this TextCanvas into quarter block characters.
      # The height of the resulting TextCanvas is half what it originally was.
      # See also: Console/ANSIBitmap.rogue
      #
      # Example:
      #   uses Console/TextCanvas
      #   local st = @|######
      #               |#     #   ####    ####   #    #  ######
      #               |#     #  #    #  #    #  #    #  #
      #               |######   #    #  #       #    #  #####
      #               |#   #    #    #  #  ###  #    #  #
      #               |#    #   #    #  #    #  #    #  #
      #               |#     #   ####    ####    ####   ######
      #
      #   println TextCanvas( st.split('\n') ).pixellated
      #
      # Output:
      #
      #   █▀▀▀▀▀▄   ▄▄▄▄    ▄▄▄▄   ▄    ▄  ▄▄▄▄▄▄
      #   █▄▄▄▄▄▀  █    █  █    ▀  █    █  █▄▄▄▄
      #   █   ▀▄   █    █  █  ▀▀█  █    █  █
      #   ▀     ▀   ▀▀▀▀    ▀▀▀▀    ▀▀▀▀   ▀▀▀▀▀▀
      local w = width
      local h = height
      if (h & 1)
        ++h
        return this.cloned.[resize(w,h)].pixellated
      endIf

      local ch00 = StyledCharacter( ' ', style )
      local ch01 = StyledCharacter( '\[2584]', style )
      local ch10 = StyledCharacter( '\[2580]', style )
      local ch11 = StyledCharacter( '\[2588]', style )

      local result = TextCanvas( w, h/2 )
      local dj = 0
      forEach (j in 0..<h step 2)
        local di = 0
        forEach (i in 0..<w)
          if (get(i,j).value == ' ')
            if (get(i,j+1).value == ' ') result.set( di, dj, ch00 )
            else                         result.set( di, dj, ch01 )
          elseIf (get(i,j+1).value == ' ')
            result.set( di, dj, ch10 )
          else
            result.set( di, dj, ch11 )
          endIf
          ++di
        endForEach
        ++dj
      endForEach

      return result

    method resize( new_size:XY )
      resize( new_size.x, new_size.y )

    method resize( new_width:Int, new_height:Int )
      if (width == new_width and height == new_height) return
      expand( 0, 0, new_width-width, new_height-height )

    method rotated_180->TextCanvas
      return flipped( &h, &v )

    method rotated_90( &cw, &ccw )->TextCanvas
      if (not ccw) cw = true
      local result = TextCanvas( height, width )

      local iterator = this.buffer.iterator
      if (ccw)
        forEach (i in 0..<result.width)
          forEach (j in result.height-1 downTo 0)
            result.set( i, j, iterator.read )
          endForEach
        endForEach
      else
        forEach (i in result.width-1 downTo 0)
          forEach (j in 0..<result.height)
            result.set( i, j, iterator.read )
          endForEach
        endForEach
      endIf

      return result

    method set( i:Int, j:Int, ch:Character )
      local clip = which{ this.clip || Box(0,0,width,height) }
      if (i < clip.x or j < clip.y or i >= clip.x2 or j >= clip.y2)
        return
      endIf
      buffer[ j*width + i ] = StyledCharacter(ch,style.current)

    method set( i:Int, j:Int, ch:StyledCharacter )
      local clip = which{ this.clip || Box(0,0,width,height) }
      if (i < clip.x or j < clip.y or i >= clip.x2 or j >= clip.y2) return
      buffer[ j*width + i ] = ch

    method set( xy:XY, ch:Character )
      set( xy.x, xy.y, ch )

    method set( xy:XY, ch:StyledCharacter )
      set( xy.x, xy.y, ch )

    method set_clip( new_clip:Box? )
      flush
      @clip = new_clip

    method set_cursor( new_pos:XY )
      flush
      @cursor = new_pos

    method shift( dx:Int, dy:Int, fill_ch=' ':Character )
      if (dx and dy)
        shift( dx, 0, fill_ch )
        shift( 0, dy, fill_ch )
        return
      elseIf (dx == 0 and dy == 0)
        return
      endIf

      if (dx > 0 or dy > 0)
        # Iterate backwards, copying forward
        local offset = dy*width + dx
        local index = (width * height) - offset
        while (index > 0)
          --index
          buffer[index+offset] = buffer[index]
        endWhile
        fill( Box(0,0,which{dx||width},which{dy||height}), fill_ch )
      else
        # Iterate forward, copying backward
        local offset = -(dy*width + dx)
        local index = offset
        local limit = width * height
        while (index < limit)
          buffer[index-offset] = buffer[index]
          ++index
        endWhile
        if (dx)
          fill( Box(width+dx,0,-dx,height), fill_ch )
        else
          fill( Box(0,height+dy,width,-dy), fill_ch )
        endIf
      endIf

    method size->XY
      return XY(width,height)

    method style->CurrentCharacterStyle
      flush
      return @style

    method to->String
      flush
      local builder = String()
      display( builder )
      return builder

endClass

class StyledCharacter( value=' ':Character, style=CharacterStyle.NONE:CharacterStyle ) [compound]
  GLOBAL METHODS
    method create( value:Character, color:Color, &fg, &bg )->StyledCharacter
      if (bg)
        return StyledCharacter( value, CharacterStyle(CharacterStyle.TRUECOLOR_BG,&bg=color) )
      endIf
      if (fg or not bg)
        return StyledCharacter( value, CharacterStyle(CharacterStyle.TRUECOLOR_FG,&fg=color) )
      endIf

    method create( value:Character, fg:Color, bg:Color )->StyledCharacter
      return StyledCharacter( value, CharacterStyle(CharacterStyle.TRUECOLOR_FG|CharacterStyle.TRUECOLOR_BG,fg,bg) )

  METHODS
    method operator==( other:StyledCharacter )->Logical
      return (value == other.value and style == other.style)
endClass

class CurrentCharacterStyle
  PROPERTIES
    current : CharacterStyle

  METHODS
    method clear
      current = CharacterStyle.NONE

    method reset
      clear

    method bg( color:Color )
      current = current.bg( color )

    method bg( color:ANSIColor )
      current = current.bg( (color.index + 1):<<:CharacterStyle.BG_SHIFT )

    method bg( color:Int )
      current = current.bg( color )

    method bold
      current += CharacterStyle.BOLD

    method colors( fg:Color, bg:Color )
      current = current.colors( fg, bg )

    method dim
      current += CharacterStyle.DIM

    method underline
      current += CharacterStyle.UNDERLINE

    method blink
      current += CharacterStyle.BLINK

    method inverse
      current += CharacterStyle.INVERSE

    method hidden
      current += CharacterStyle.HIDDEN

    method bold_off
      current -= CharacterStyle.BOLD

    method dim_off
      current -= CharacterStyle.DIM

    method underline_off
      current -= CharacterStyle.UNDERLINE

    method blink_off
      current -= CharacterStyle.BLINK

    method inverse_off
      current -= CharacterStyle.INVERSE

    method hidden_off
      current -= CharacterStyle.HIDDEN

    method fg( color:Color )
      current = current.fg( color )

    method fg( color:ANSIColor )
      current = current.fg( (color.index + 1):<<:CharacterStyle.FG_SHIFT )

    method fg( color:Int )
      current = current.fg( color )

    method fg_default
      current = current.fg( CharacterStyle.FG_DEFAULT )

    method fg_black
      current = current.fg( CharacterStyle.FG_BLACK )

    method fg_red
      current = current.fg( CharacterStyle.FG_RED )

    method fg_green
      current = current.fg( CharacterStyle.FG_GREEN )

    method fg_yellow
      current = current.fg( CharacterStyle.FG_YELLOW )

    method fg_blue
      current = current.fg( CharacterStyle.FG_BLUE )

    method fg_magenta
      current = current.fg( CharacterStyle.FG_MAGENTA )

    method fg_cyan
      current = current.fg( CharacterStyle.FG_CYAN )

    method fg_light_gray
      current = current.fg( CharacterStyle.FG_LIGHT_GRAY )

    method fg_dark_gray
      current = current.fg( CharacterStyle.FG_DARK_GRAY )

    method fg_light_red
      current = current.fg( CharacterStyle.FG_LIGHT_RED )

    method fg_light_green
      current = current.fg( CharacterStyle.FG_LIGHT_GREEN )

    method fg_light_yellow
      current = current.fg( CharacterStyle.FG_LIGHT_YELLOW )

    method fg_light_blue
      current = current.fg( CharacterStyle.FG_LIGHT_BLUE )

    method fg_light_magenta
      current = current.fg( CharacterStyle.FG_LIGHT_MAGENTA )

    method fg_light_cyan
      current = current.fg( CharacterStyle.FG_LIGHT_CYAN )

    method fg_white
      current = current.fg( CharacterStyle.FG_WHITE )

    method bg_default
      current = current.bg( CharacterStyle.BG_DEFAULT )

    method bg_black
      current = current.bg( CharacterStyle.BG_BLACK )

    method bg_red
      current = current.bg( CharacterStyle.BG_RED )

    method bg_green
      current = current.bg( CharacterStyle.BG_GREEN )

    method bg_yellow
      current = current.bg( CharacterStyle.BG_YELLOW )

    method bg_blue
      current = current.bg( CharacterStyle.BG_BLUE )

    method bg_magenta
      current = current.bg( CharacterStyle.BG_MAGENTA )

    method bg_cyan
      current = current.bg( CharacterStyle.BG_CYAN )

    method bg_light_gray
      current = current.bg( CharacterStyle.BG_LIGHT_GRAY )

    method bg_dark_gray
      current = current.bg( CharacterStyle.BG_DARK_GRAY )

    method bg_light_red
      current = current.bg( CharacterStyle.BG_LIGHT_RED )

    method bg_light_green
      current = current.bg( CharacterStyle.BG_LIGHT_GREEN )

    method bg_light_yellow
      current = current.bg( CharacterStyle.BG_LIGHT_YELLOW )

    method bg_light_blue
      current = current.bg( CharacterStyle.BG_LIGHT_BLUE )

    method bg_light_magenta
      current = current.bg( CharacterStyle.BG_LIGHT_MAGENTA )

    method bg_light_cyan
      current = current.bg( CharacterStyle.BG_LIGHT_CYAN )

    method bg_white
      current = current.bg( CharacterStyle.BG_WHITE )

endClass

class CharacterStyle( flags:Int, fg=Color(0):Color, bg=Color(0):Color ) [compound]
  GLOBAL PROPERTIES
    is_truecolor_supported = ((System.is_windows and Windows.is_windows_10_or_greater) or
                               System.env//COLORTERM == "truecolor") : Logical

  DEFINITIONS
    NONE                 = CharacterStyle(0)

    FG_MASK              = 0b000000000_111111111_00000000
    BG_MASK              = 0b111111111_000000000_00000000
    FG_SHIFT             = 8
    BG_SHIFT             = 17

    BOLD                 = (1 :<<: 0)
    DIM                  = (1 :<<: 1)
    UNDERLINE            = (1 :<<: 2)
    BLINK                = (1 :<<: 3)
    INVERSE              = (1 :<<: 4)
    HIDDEN               = (1 :<<: 5)
    TRUECOLOR_FG         = (1 :<<: 6)
    TRUECOLOR_BG         = (1 :<<: 7)
    TRUECOLOR            = (CharacterStyle.TRUECOLOR_FG | CharacterStyle.TRUECOLOR_BG)

    FG_DEFAULT           = (0  :<<: 8)
    FG_BLACK             = (1  :<<: 8)
    FG_RED               = (2  :<<: 8)
    FG_GREEN             = (3  :<<: 8)
    FG_YELLOW            = (4  :<<: 8)
    FG_BLUE              = (5  :<<: 8)
    FG_MAGENTA           = (6  :<<: 8)
    FG_CYAN              = (7  :<<: 8)
    FG_LIGHT_GRAY        = (8  :<<: 8)
    FG_DARK_GRAY         = (9  :<<: 8)
    FG_LIGHT_RED         = (10 :<<: 8)
    FG_LIGHT_GREEN       = (11 :<<: 8)
    FG_LIGHT_YELLOW      = (12 :<<: 8)
    FG_LIGHT_BLUE        = (13 :<<: 8)
    FG_LIGHT_MAGENTA     = (14 :<<: 8)
    FG_LIGHT_CYAN        = (15 :<<: 8)
    FG_WHITE             = (16 :<<: 8)
    FG_EXTENDED          = (17 :<<: 8)
    # (17..256) :<<: 6 - extended 256 fg colors

    BG_DEFAULT           = (0  :<<: 17)
    BG_BLACK             = (1  :<<: 17)
    BG_RED               = (2  :<<: 17)
    BG_GREEN             = (3  :<<: 17)
    BG_YELLOW            = (4  :<<: 17)
    BG_BLUE              = (5  :<<: 17)
    BG_MAGENTA           = (6  :<<: 17)
    BG_CYAN              = (7  :<<: 17)
    BG_LIGHT_GRAY        = (8  :<<: 17)
    BG_DARK_GRAY         = (9  :<<: 17)
    BG_LIGHT_RED         = (10 :<<: 17)
    BG_LIGHT_GREEN       = (11 :<<: 17)
    BG_LIGHT_YELLOW      = (12 :<<: 17)
    BG_LIGHT_BLUE        = (13 :<<: 17)
    BG_LIGHT_MAGENTA     = (14 :<<: 17)
    BG_LIGHT_CYAN        = (15 :<<: 17)
    BG_WHITE             = (16 :<<: 17)
    BG_EXTENDED          = (17 :<<: 17)
    # (17..256) :<<: 6 - extended 256 bg colors

  GLOBAL PROPERTIES
    fg_colors =
    Int[][
      ConsoleStyle.FG_DEFAULT,
      ConsoleStyle.FG_BLACK,
      ConsoleStyle.FG_RED,
      ConsoleStyle.FG_GREEN,
      ConsoleStyle.FG_YELLOW,
      ConsoleStyle.FG_BLUE,
      ConsoleStyle.FG_MAGENTA
      ConsoleStyle.FG_CYAN,
      ConsoleStyle.FG_LIGHT_GRAY,
      ConsoleStyle.FG_DARK_GRAY,
      ConsoleStyle.FG_LIGHT_RED,
      ConsoleStyle.FG_LIGHT_GREEN,
      ConsoleStyle.FG_LIGHT_YELLOW,
      ConsoleStyle.FG_LIGHT_BLUE,
      ConsoleStyle.FG_LIGHT_MAGENTA,
      ConsoleStyle.FG_LIGHT_CYAN,
      ConsoleStyle.FG_WHITE
    ]

    bg_colors =
    Int[][
      ConsoleStyle.BG_DEFAULT,
      ConsoleStyle.BG_BLACK,
      ConsoleStyle.BG_RED,
      ConsoleStyle.BG_GREEN,
      ConsoleStyle.BG_YELLOW,
      ConsoleStyle.BG_BLUE,
      ConsoleStyle.BG_MAGENTA
      ConsoleStyle.BG_CYAN,
      ConsoleStyle.BG_LIGHT_GRAY,
      ConsoleStyle.BG_DARK_GRAY,
      ConsoleStyle.BG_LIGHT_RED,
      ConsoleStyle.BG_LIGHT_GREEN,
      ConsoleStyle.BG_LIGHT_YELLOW,
      ConsoleStyle.BG_LIGHT_BLUE,
      ConsoleStyle.BG_LIGHT_MAGENTA,
      ConsoleStyle.BG_LIGHT_CYAN,
      ConsoleStyle.BG_WHITE
    ]

  METHODS
    method apply( new_style:CharacterStyle, output=Global:PrintWriter )->CharacterStyle
      block new_flags=new_style.flags
        local diff = this.flags ~ new_flags
        if (diff)
          if (diff & BOLD)
            if (new_flags & BOLD) ConsoleStyle.set<<BOLD>>( output )
            else                  ConsoleStyle.set<<BOLD_OFF>>( output )
          endIf
          if (diff & DIM)
            if (new_flags & DIM) ConsoleStyle.set<<DIM>>( output )
            else                 ConsoleStyle.set<<DIM_OFF>>( output )
          endIf
          if (diff & UNDERLINE)
            if (new_flags & UNDERLINE) ConsoleStyle.set<<UNDERLINE>>( output )
            else                       ConsoleStyle.set<<UNDERLINE_OFF>>( output )
          endIf
          if (diff & BLINK)
            if (new_flags & BLINK) ConsoleStyle.set<<BLINK>>( output )
            else                   ConsoleStyle.set<<BLINK_OFF>>( output )
          endIf
          if (diff & INVERSE)
            if (new_flags & INVERSE) ConsoleStyle.set<<INVERSE>>( output )
            else                     ConsoleStyle.set<<INVERSE_OFF>>( output )
          endIf
          if (diff & HIDDEN)
            if (new_flags & HIDDEN) ConsoleStyle.set<<HIDDEN>>( output )
            else                    ConsoleStyle.set<<HIDDEN_OFF>>( output )
          endIf

          if (is_truecolor_supported)
            if ((new_flags & (TRUECOLOR_FG|TRUECOLOR_BG)) == (TRUECOLOR_FG|TRUECOLOR_BG))
              # Truecolor has been newly set for both FG and BG.
              # Apply truecolor values and don't apply 256-color values.
              ConsoleStyle.colors( new_style.fg, new_style.bg, output )
              diff &= !(FG_MASK | BG_MASK)
            else
              if (new_flags & TRUECOLOR_FG)
                # We just turned on truecolor FG.
                # Apply truecolor FG value and don't apply 256-color FG value.
                ConsoleStyle.fg_color( new_style.fg, output )
                diff &= !FG_MASK
              elseIf (diff & TRUECOLOR_FG)
                # We just turned off truecolor FG; be sure to apply 256-color FG
                diff |= FG_MASK
              endIf

              if (new_flags & TRUECOLOR_BG)
                # We just turned on truecolor BG.
                # Apply truecolor BG value and don't apply 256-color BG value.
                ConsoleStyle.bg_color( new_style.bg, output )
                diff &= !BG_MASK
              elseIf (diff & TRUECOLOR_BG)
                # We just turned off truecolor BG; be sure to apply 256-color BG
                diff |= BG_MASK
              endIf

            endIf
          endIf

          if (diff & FG_MASK)
            local fg = (new_flags & FG_MASK) :>>>: FG_SHIFT
            if (fg >= 17)
              ConsoleStyle.fg_color( fg-1, output )
            else
              ConsoleStyle.set( fg_colors[fg], output )
            endIf
          endIf

          if (diff & BG_MASK)
            local bg = (new_flags & BG_MASK) :>>>: BG_SHIFT
            if (bg >= 17)
              ConsoleStyle.bg_color( bg-1, output )
            else
              ConsoleStyle.set( bg_colors[bg], output )
            endIf
          endIf

        else
          # No flags changed but if we're in a truecolor mode then check
          # for altered colors
          if ((new_flags & TRUECOLOR_FG) and is_truecolor_supported)
            if (new_style.fg.argb != this.fg.argb)
              ConsoleStyle.fg_color( new_style.fg, output )
            endIf
          endIf

          if ((new_flags & TRUECOLOR_BG) and is_truecolor_supported)
            if (new_style.bg.argb != this.bg.argb)
              ConsoleStyle.bg_color( new_style.bg, output )
            endIf
          endIf

        endIf
      endBlock
      return new_style

    method bg( bg:Color )->CharacterStyle
      return CharacterStyle( flags|TRUECOLOR_BG, fg, bg )

    method bg( color:Int )->CharacterStyle
      return CharacterStyle( (flags & !(BG_MASK|TRUECOLOR_BG)) | color, fg, bg )

    method colors( fg:Color, bg:Color )->CharacterStyle
      return CharacterStyle( flags|TRUECOLOR_FG|TRUECOLOR_BG, fg, bg )

    method fg( fg:Color )->CharacterStyle
      return CharacterStyle( flags|TRUECOLOR_FG, fg, bg )

    method fg( color:Int )->CharacterStyle
      return CharacterStyle( (flags & !(FG_MASK|TRUECOLOR_FG)) | color, fg, bg )

    method operator+( flag:Int )->CharacterStyle
      return CharacterStyle( flags | flag, fg, bg )

    method operator-( flag:Int )->CharacterStyle
      return CharacterStyle( flags & !flag, fg, bg )

    method operator==( other:CharacterStyle )->Logical
      return (flags == other.flags and fg.argb == other.fg.argb and bg.argb == other.bg.argb)

    method reset
      ConsoleStyle.reset
endClass

augment Graphics::Bitmap
  METHODS
    method to->TextCanvas( &dither, bg8=CharacterStyle.BG_BLACK:Int, bg24=Color.BLACK:Color )
      contingent
        sufficient (System.is_windows)
        necessary  (System.env//COLORTERM == "truecolor")
      unsatisfied
        return ANSIBitmap( this, &=dither )->TextCanvas( bg8 )
      endContingent

      localize width
      localize height
      local canvas = TextCanvas( width, height/2+(height&1) )
      local canvas_data = canvas.buffer

      local evens = Bitmap.transparent_checkerboard_evens
      local odds = Bitmap.transparent_checkerboard_odds

      local src_index = 0
      local dest_index = 0

      if (height & 1)
        # First line contains default BG color on the top and the first row of color data on the bottom.
        forEach (i in 0..<width)
          local fg = pixels[src_index]
          if (fg.alpha_byte < 255)
            if (i & 2) fg = evens * fg
            else       fg = odds  * fg
          endIf
          canvas_data[ dest_index ] = StyledCharacter( '\[2584]', fg, bg24 )
          ++src_index
          ++dest_index
        endForEach
      endIf

      forEach (j in 0..<height/2)
        forEach (i in 0..<width)
          local bg = pixels[src_index]
          local fg = pixels[src_index+width]
          if (bg.alpha_byte < 255 or fg.alpha_byte < 255)
            if ((i & 2) xor (j & 1))
              bg = evens * bg
              fg = odds  * fg
            else
              bg = odds  * bg
              fg = evens * fg
            endIf
          endIf
          canvas_data[ dest_index ] = StyledCharacter( '\[2584]', fg, bg )
          ++src_index
          ++dest_index
        endForEach
        src_index += width
      endForEach

      return canvas
endAugment

