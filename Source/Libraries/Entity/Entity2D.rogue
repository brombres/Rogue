library Entity

class Entity2D : Entity
  PROPERTIES
    nominal_size : XY
    scale        = XY(1,1)
    position     : XY
    z            : Real
    rotation     : Radians
    anchor       = Anchor.CENTER : Anchor

  METHODS
    method init
      nominal_size = XY(1,1)

    method init( script:EntityScript )
      init
      attach_script( script )

    method init( nominal_size, script=null:EntityScript )
      if (script) attach_script( script )

    method init( existing:Entity2D )
      prior.init( existing )
      @size         = existing.size
      @nominal_size = existing.nominal_size
      @position     = existing.position
      @z            = existing.z
      @rotation     = existing.rotation
      @anchor       = existing.anchor

    method scale_to_fill( bounds:Box )
      scale = (bounds.size / nominal_size).max

    method scale_to_fit( bounds:Box )
      scale = (bounds.size / nominal_size).min

    method set_rotation( @rotation )

    method set_rotation( degrees:Degrees )
      @rotation = degrees->Radians

    method set_rotation( degrees:Real )
      @rotation = Degrees(degrees)->Radians

    method set_scale( scale:Real )
      @scale = scale

    method set_scale( @scale )
      noAction

    method set_size( nominal_size )
      scale = XY(1.0)

    method size->XY
      return nominal_size * scale

endClass
