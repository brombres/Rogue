library Entity

class Entity2D : Entity
  PROPERTIES
    size          : XY
    internal_size : XY
    position      : XY
    z             : Real
    rotation      : Radians
    rotation_3d   : Quaternion?
    anchor        = Anchor.CENTER : Anchor

  METHODS
    method scale->XY
      return size / internal_size

    method scale_to_fill( bounds:Box )
      scale = (bounds.size / internal_size).max

    method scale_to_fit( bounds:Box )
      scale = (bounds.size / internal_size).min

    method set_rotation( @rotation )
      rotation_3d = null

    method set_rotation( degrees:Degrees )
      @rotation = degrees->Radians
      rotation_3d = null

    method set_rotation( degrees:Real )
      @rotation = Degrees(degrees)->Radians
      rotation_3d = null

    method set_rotation( degrees:DegreesXYZ )
      rotation_3d = Quaternion( degrees->RadiansXYZ )

    method set_rotation( radians:RadiansXYZ )
      rotation_3d = Quaternion( radians )

    method set_scale( scale:Real )
      size = internal_size * scale

    method set_scale( scale:XY )
      size = internal_size * scale

endClass
