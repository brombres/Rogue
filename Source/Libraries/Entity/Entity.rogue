library Entity

uses Collection/Node
uses Control
uses Geometry
uses Math/Quaternion

$include Entity/Entity2D
$include Entity/Entity3D
$include Entity/Attributes
$include Entity/EntityScript
$include Entity/Properties

class Entity : Node<<Entity>>
  GLOBAL PROPERTIES
    next_id = 1

  PROPERTIES
    name       : String
    id         : Int
    attributes : Attributes
    script     : EntityScript
    actions    : Action
    properties : Variant

  METHODS
    method init
      noAction

    method init( name )
      noAction

    method init( existing:Entity )
      name        = existing.name
      attributes  = existing.attributes
      script      = existing.script
      @properties = existing.@properties.cloned

    method cloned->ThisType [propagated]
      return ThisType( this )

    method init_object
      id = next_id
      ++next_id

    method attach_script( new_script:EntityScript )
      new_script.entity = this
      script |= new_script.started

    method change_script( new_script:EntityScript )
      new_script.entity = this
      if (script) script.change_script( new_script )
      else        script = new_script.started

    method draw
      if (attributes.hidden) return
      .begin_draw
      .handle_draw( &bg )
      .handle_draw
      .handle_draw( &!bg )

    method draw( &bg )
      if (bg == not attributes.foreground) .draw_entity

    method h_flip->Logical
      return attributes.h_flip

    method on_draw
      noAction

    method on_draw( &bg )
      noAction

    method on_start
      noAction

    method on_update( dt:Real )
      noAction

    method properties->Properties
      return Properties( this )

    method set_h_flip( setting:Logical )
      attributes.h_flip = setting

    method set_is_visible( setting:Logical )
      attributes.hidden = not setting

    method set_v_flip( setting:Logical )
      attributes.v_flip = setting

    method start
      if (not attributes.started)
        attributes.started = true
        script = script?.started
        on_start
      endIf

    method to->String
      if (@name) return @name
      else       return type_name

    method update( dt:Real )
      if (not attributes.started) start

      script = script?.updated
      if (actions) actions = actions.updated
      on_update( dt )
      (forEach in children).update( dt )

    method v_flip->Logical
      return attributes.v_flip

    method is_visible->Logical
      return not attributes.hidden

    method .begin_draw
      if (not attributes.started) start

    method .dispatch_draw
      if (count)
        (forEach in children).draw
      endIf

    method .handle_draw
      if (attributes.hidden) return

      on_draw

      script?.call<<on_draw>>
      actions?.call<<on_draw>>

      .dispatch_draw

    method .handle_draw( &bg )
      if (attributes.hidden) return

      if (bg)
        actions?.call<<on_draw,Logical>>( true )
        script?.call<<on_draw,Logical>>( true )

        on_draw( &bg )

        draw( &bg )
      else
        draw( &!bg )

        on_draw( &!bg )

        script?.call<<on_draw,Logical>>( false )
        actions?.call<<on_draw,Logical>>( false )
      endIf

    method .draw_entity
      noAction
endClass
