$include UI/Keyboard
uses UI

class Event
  PROPERTIES
    is_consumed  : Logical
    refcount     : NewInt
    timestamp    : NewInt

  METHODS
    method init( timestamp=System.execution_time_ms )
      is_consumed = false
      refcount    = 0

    method init( existing:Event )
      is_consumed = existing.is_consumed
      refcount    = existing.refcount
      timestamp   = existing.timestamp

    method cloned->Event [propagated]
      return ThisType( this )

    method consume
      is_consumed = true

    method release [propagated]
      --refcount
      if (refcount == 0)
        ObjectPool<<ThisType>>.on_end_use( this )
      endIf

    method retain
      ++refcount
endClass

class KeyEvent : Event
  PROPERTIES
    keycode            : NewInt   # if keycode == Keycode.UNICODE then 'syscode' contains unicode value
    syscode            : NewInt
    is_press           : Logical
    is_repeat          : Logical
    modifier_key_state : NewInt

  METHODS
    method init( keycode, syscode, is_press=false, is_repeat=false, modifier_key_state=Keyboard.modifier_key_state )

    method cloned->KeyEvent
      return KeyEvent( keycode, syscode, is_press, is_repeat, modifier_key_state ).[ init(this) ]

    method is_alt_pressed->Logical
      return (modifier_key_state & Keyboard.ALT_MODIFIER_FLAG)

    method is_control_pressed->Logical
      return (modifier_key_state & Keyboard.CONTROL_MODIFIER_FLAG)

    method is_os_pressed->Logical
      return (modifier_key_state & Keyboard.OS_MODIFIER_FLAG)

    method is_press( _keycode:NewInt )->Logical
      return keycode == _keycode and is_press

    method is_repeat( _keycode:NewInt )->Logical
      return keycode == _keycode and is_repeat

    method is_release->Logical
      return not this.is_press

    method is_release( _keycode:NewInt )->Logical
      return keycode == _keycode and not is_press

    method is_shift_pressed->Logical
      return (modifier_key_state & Keyboard.SHIFT_MODIFIER_FLAG)

    method to->String
      local buffer = String()
      which (keycode)
         case    1: buffer.print( "[LeftArrow] " )
         case    2: buffer.print( "[UpArrow] " )
         case    3: buffer.print( "[RightArrow] " )
         case    4: buffer.print( "[DownArrow] " )
         case    8: buffer.print( "[Backspace] " )
         case '\t': buffer.print( "[Tab] " )
         case '\n': buffer.print( "[Enter] " )
         case   27: buffer.print( "[Escape] " )
         case   32: buffer.print( "[Space] " )
         case  127: buffer.print( "[Delete] " )
         others
           buffer.[ print("(Keycode "), print(NewInt(keycode)), print(") ") ]
       endWhich

       if (is_press)
         if (is_repeat) buffer.print( "pressed (repeating)" )
         else           buffer.print( "pressed" )
       else
         buffer.print( "released" )
       endIf

        if (is_consumed) buffer.print( " (consumed)" )

       return buffer

    method unicode->Character
      if (keycode == Keycode.UNICODE) return syscode->Character
      return Keycode.keycode_to_unicode( keycode, is_shift_pressed )

endClass


class TextEvent : Event
  PROPERTIES
    character : Character
    text      : String

  METHODS
    method init( character, text )

    method cloned->TextEvent
      return TextEvent( character, text ).[ init(this) ]

    method text->String
      if (not @text) text = "" + character
      return @text

    method count->NewInt
      if (@text) return @text.count
      else       return 1

    method operator==( ch:Character )->Logical
      return (this.character == ch and (@text is null or @text.count == 1))

    method operator==( value:String )->Logical
      which (value.count)
        case 0
          return false
        case 1
          return (character == value[0])
        case 2
          return (text == value)
      endWhich

    method to->String
      return text
endClass


class PointerEvent : Event
  DEFINITIONS
    MOVE       = 0
    PRESS      = 1
    RELEASE    = 2
    DRAG_MOVE  = 3
    DRAG_START = 4
    DRAG_STOP  = 5

    LEFT     = 0
    RIGHT    = 1
    MIDDLE   = 2

  PROPERTIES
    type      : NewInt
    position  : IntXY
    delta     : IntXY
    index     : NewInt
    cancelled : Logical
    # When a touch event in progress is interrupted then a RELEASE event is sent
    # with .cancelled==true.

  METHODS
    method init( type, position, delta, index, cancelled=false )

    method init( existing:PointerEvent )
      type = existing.type
      position = existing.position
      delta = existing.delta
      index = existing.index
      cancelled = existing.cancelled

    method cloned->PointerEvent
      return PointerEvent( this )

    method is_drag_move->Logical
      return (this.type == PointerEvent.DRAG_MOVE)

    method is_drag_start->Logical
      return (this.type == PointerEvent.DRAG_START)

    method is_drag_stop->Logical
      return (this.type == PointerEvent.DRAG_STOP)

    method is_move->Logical
      return (this.type == PointerEvent.MOVE)

    method is_press->Logical
      return (this.type == PointerEvent.PRESS)

    method is_press( button_index:NewInt )->Logical
      return (this.type == PRESS and index == button_index)

    method is_press_left->Logical
      return is_press( 0 )

    method is_press_right->Logical
      return is_press( 1 )

    method is_press_middle->Logical
      return is_press( 2 )

    method is_release->Logical
      return (this.type == PointerEvent.RELEASE)

    method is_release( button_index:NewInt )->Logical
      return (this.type == RELEASE and index == button_index)

    method is_release_left->Logical
      return is_release( 0 )

    method is_release_right->Logical
      return is_release( 1 )

    method is_release_middle->Logical
      return is_release( 2 )

    method operator+( offset:IntXY )->PointerEvent
      return PointerEvent( type, position+offset, delta, index, cancelled )

    method operator-( offset:IntXY )->PointerEvent
      return this + (-offset)

    method to->String
      local buffer = String()
      buffer.print( "Pointer " )
      which (type)
        case MOVE:       buffer.print( "move" )
        case PRESS:      buffer.[ print("press index "), print(index) ]
        case RELEASE:    buffer.print( "release" )
        case DRAG_START: buffer.print( "drag start" )
        case DRAG_MOVE:  buffer.print( "drag move" )
        case DRAG_STOP:  buffer.print( "drag stop" )
      endWhich
      buffer.[ print(" at "), print(position) ]
      if (is_consumed) buffer.print( " (consumed)" )

      return buffer
endClass


class ScrollEvent : Event
  PROPERTIES
    delta         : IntXY
    precise       : Logical
      # True for Apple Magic Mouse and Trackpad, AKA supports gestures and momentum.
      # If this is true then is_progress and is_momentum are valid (and vice versa).
    in_progress   : Logical
      # macOS: in_progress means that either the finger is still resting on the Magic Mouse/trackpad
      # or that a "momentum scroll" is active. Either way scroll events are guaranteed to continue
      # to be received until one where 'in_progress' is false.
    is_momentum   : Logical
    density       : NewInt

  METHODS
    method init( delta, precise=false, in_progress=false, is_momentum=false, density=1 )

    method cloned->ScrollEvent
      return ScrollEvent( delta, &=precise, &=in_progress, &=is_momentum, &=density ).[ init(this) ]

    method to->String
      local buffer = String().[ print("Scroll "), print( delta ) ]
      buffer.print( " [$x]"(density) )
      if (in_progress) buffer.print( " [in progress]" )
      if (is_momentum) buffer.print( " [momentum]" )
      if (is_consumed) buffer.print( " (consumed)" )
      return buffer
endClass

class ScrollRate
  # macOS with Magic Mouse or trackpad:
  # - Tracks 0.25 seconds of continuous-gesture scroll data and accelerates
  #   the scroll deltas to allow behavior similar to built-in OS scrolling.
  #
  # Other OS or input device:
  # - Scales each scroll delta by 'base_scale' with no acceleration.
  PROPERTIES
    base_scale     : NewInt
    delta          : IntXY
    history_x      = ScrollEvent[]
    history_y      = ScrollEvent[]
    acceleration_x = 1.0
    acceleration_y = 1.0

  METHODS
    method init( base_scale=5 )

    method add( e:ScrollEvent )->IntXY
      # Returns the scaled or accelerated scroll-delta.
      contingent
        necessary (e.precise)
        necessary (not e.is_momentum)

        delta = IntXY(
          _process<<history_x,x,acceleration_x>>(e),
          _process<<history_y,y,acceleration_y>>(e)
        )

      unsatisfied
        delta = e.delta * base_scale
        if (not e.precise) delta *= e.density
        history_x.clear
        history_y.clear
        acceleration_x = 1.0
        acceleration_y = 1.0

      endContingent

      return delta

    method _same_direction( v1:NewReal, v2:NewReal )->Logical
      if (v1 == 0 or v2 == 0) return true  # 0 matches either sign
      return not ((v1 > 0) xor (v2 > 0))

    method _process<<$history,$xy,$acceleration>>( e:ScrollEvent )->NewReal
      while ($history.count and $history.first.timestamp < e.timestamp - 250) $history.remove_first

      local result = 0.0
      if ($history.count and _same_direction($history.first.delta.$xy,e.delta.$xy))
        $history.add( e )
        result += (forEach in $history).delta.$xy
        result /= $history.count
        if ($history.count == 1)
          $acceleration = 1.0
        else
          result *= $acceleration
        endIf
      else
        $history.[ clear, add(e) ]
        result = e.delta.$xy
        $acceleration = 1.0
      endIf

      $acceleration = ($acceleration * 1.25).clamped_high( base_scale*2 )
      return result
endClass
