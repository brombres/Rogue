library UI

uses Collection/Node
uses Control
uses Entity
uses Geometry

$include UI/Border
$include UI/Component
$include UI/ComponentSizeInfo
$include UI/Event
$include UI/Font
$include UI/HBox
$include UI/HFlow
$include UI/Label
$include UI/LayoutMode
$include UI/MetaComponent
$include UI/ParameterNames
$include UI/Text
$include UI/TextBox
$include UI/TextStyle
$include UI/VBox
$include UI/VList
$include UI/VScrollView
$include UI/Widget

class UI : Component [singleton]
  PROPERTIES
    default_font   : Font

    clip           : Box?

    pointer_focus  : Component
    keyboard_focus : Component

    parameters     : Variant

    needs_layout   = true
    needs_redraw   = true

    snap_to_pixel_coordinates = true

    update_timer = UpdateTimer()

  METHODS
    method init_object
      UI = this

    method drag_threshold->Real
      return 16

    method draw [override]
      temporarily UI = this
        prior.draw
      endTemporarily

    method filler->Component
      return Component()

    method font->Font
      return default_font

    method handle( e:KeyEvent )
      temporarily UI = this
        on( e )
        if (e.is_consumed) return

        if (keyboard_focus)
          if (not keyboard_focus.attributes.hidden and not keyboard_focus.attributes.input_disabled)
            keyboard_focus.handle( e )
          else
            keyboard_focus.release_keyboard_focus
            first_child_component?.dispatch( e )
          endIf
        else
          first_child_component?.dispatch( e )
        endIf
        if (e.is_consumed) return

        on_unhandled( e )
      endTemporarily

    method handle( e:PointerEvent )
      temporarily UI = this
        UI = this

        on( e )
        if (e.is_consumed) return

        if (pointer_focus)
          if (not pointer_focus.attributes.hidden and not pointer_focus.attributes.input_disabled)
            pointer_focus.handle( e )
          else
            pointer_focus.release_pointer_focus
            first_child_component?.dispatch( e )
          endIf
        else
          first_child_component?.dispatch( e )
        endIf
        if (e.is_consumed) return

        on_unhandled( e )
      endTemporarily

    method handle( e:ScrollEvent )
      temporarily UI = this
        UI = this

        on( e )
        if (e.is_consumed) return

        if (pointer_focus)
          pointer_focus.handle( e )
        else
          first_child_component?.dispatch( e )
        endIf
        if (e.is_consumed) return

        on_unhandled( e )
      endTemporarily

    method handle( e:TextEvent )
      temporarily UI = this
        UI = this

        on( e )
        if (e.is_consumed) return

        if (keyboard_focus)
          keyboard_focus.handle( e )
        else
          first_child_component?.dispatch( e )
        endIf
        if (e.is_consumed) return

        on_unhandled( e )
      endTemporarily

    method set_clip( @clip )
      noAction

    method spacer( w=0:Int, h=0:Int )->Component
      return Component( XY(w,h) )

    method update( bounds:Box )
      update_timer.update
      update( bounds, update_timer.dt )

    method update( bounds:Box, dt:Real )
      temporarily UI = this
        update_layout( bounds )
        update( dt )
      endTemporarily

    method update_layout( bounds:Box )
      temporarily UI = this
        position = bounds.position
        if (needs_layout or bounds.size != size)
          if (needs_layout)
            needs_layout = false
            needs_redraw = true
          endIf
          begin_layout
          resolve_layout_size( bounds.size )
          perform_layout( position )
          end_layout
        endIf
      endTemporarily

endClass
