library Geometry

class XY( x:Real, y:Real ) [compound]
  DEFINITIONS
    zero = XY(0,0)

  GLOBAL METHODS
    method create( r:Real, angle:Radians )->XY
      return XY( r*angle.cos, r*angle.sin )

    method create( r:Real, angle:Degrees )->XY
      local radians = angle->Radians
      return XY( r*angle.cos, r*angle.sin )

    method create( n:Real )->XY
      return XY(n,n)

    method operator+( a:XY, b:XY )->XY
      return XY( a.x+b.x, a.y+b.y )

    method operator-( a:XY, b:XY )->XY
      return XY( a.x-b.x, a.y-b.y )

    method operator*( a:XY, b:XY )->XY
      return XY( a.x*b.x, a.y*b.y )

    method operator*( a:XY, b:Real )->XY
      return XY( a.x*b, a.y*b )

    method operator/( a:XY, b:XY )->XY
      return XY( a.x/b.x, a.y/b.y )

    method operator/( a:XY, b:Real )->XY
      return XY( a.x/b, a.y/b )

    method operator/( a:Real, b:XY )->XY
      return XY( a/b.x, a/b.y )

    method operator%( a:XY, b:XY )->XY
      return XY( a.x%b.x, a.y%b.y )

    method operator%( a:XY, b:Real )->XY
      if (b == 0) return a
      return XY( a.x%b, a.y%b )

    method operator%( a:Real, b:XY )->XY
      return XY( a%b.x, a%b.y )

    method operator^( a:XY, b:XY )->XY
      return XY( a.x^b.x, a.y^b.y )

    method operator^( a:XY, b:Real )->XY
      return XY( a.x^b, a.y^b )

    method operator*( a:Real, b:XY )->XY
      return XY( a*b.x, a*b.y )

  METHODS
    method abs->XY
      return XY( x.abs, y.abs )

    method bounding_box->Box
      return Box( this )

    method ceiling->XY
      return XY( x.ceiling, y.ceiling )

    method clamped( box:Box )->XY
      # Adjusts this point to be within the boundaries of the given box.
      local vx = x
      local vy = y
      local x1 = box.x1
      local y1 = box.y1
      local x2 = box.x2
      local y2 = box.y2

      if (vx < x1)     vx = x1
      elseIf (vx > x2) vx = x2

      if (vy < y1)     vy = y1
      elseIf (vy > y2) vy = y2

      return XY(vx,vy)

    method clamped( line:Line )->XY
      # Returns the point along the given line segment that is closest
      # to this point.
      #
      # Notes:
      #
      # Points along the line are defined by:
      #
      #   Line(t) = line.a + t * (line.b - line.a)
      #
      # where t is 0 to 1.  We can define t as:
      #
      #   A = v - line.a
      #   B = line.b - line.a
      #   t = dot(A,B) / dot(B,B)
      #
      # as this is the projection of A along and proportional to B.
      local P1 = line.a
      local P2 = line.b
      local A = this - P1
      local B = P2 - P1
      local t_numer = A.dot(B)
      if (t_numer < 0) return P1
      local t_denom = B.dot(B)
      if (t_numer > t_denom) return P2
      return P1 + (t_numer / t_denom) * B

    method clamped( ray:Ray )->XY
      # Returns the point along the given ray that is closest
      # to this point.
      #
      # Notes:
      #
      # Points along the ray are defined by:
      #
      #   Ray(t) = ray.position + t * ray.direction
      #
      # where t is 0 or higher.  We can define t as:
      #
      #   A = pt - line.position
      #   B = line.direction
      #   t = dot(A,B) / dot(B,B)
      #
      # as this is the projection of A along and proportional to B.
      local A = this - ray.position
      local B = ray.direction
      local t_numer = A.dot(B)
      if (t_numer < 0) return ray.position
      local t_denom = B.dot(B)
      return ray.position + (t_numer / t_denom) * B

    method clamped( low:XY, high:XY )->XY
      return this.clamped_low(low).clamped_high(high)

    method clamped_high( limit:XY )->XY
      return XY( this.x.clamped_high(limit.x), this.y.clamped_high(limit.y) )

    method clamped_low( limit:XY )->XY
      return XY( this.x.clamped_low(limit.x), this.y.clamped_low(limit.y) )

    method cross( other:XY )->Real
      return x * other.y - y * other.x

    method degrees->Degrees
      return Radians( y.atan2(x) )->Degrees

    method distance_to( other:XY )->Int
      return (other - this).magnitude

    method dot( other:XY )->Real
      return x * other.x + y * other.y

    method floor->XY
      return XY( x.floor, y.floor )

    method hashcode->Int
      return x.hashcode ~ y.hashcode

    method is_zero->Logical
      return x == 0 and y == 0

    method magnitude->Real
      return (x*x + y*y).sqrt

    method magnitude_squared->Real
      return x*x + y*y

    method max->Real
      return which{ x>=y:x || y }

    method min->Real
      return which{ x<=y:x || y }

    method max( other:XY )->XY
      # Returns an XY containing the max X and the max Y of 'this' and 'other'.
      local max_x = which{ x>=other.x:x || other.x }
      local max_y = which{ y>=other.y:y || other.y }
      return XY( max_x, max_y )

    method min( other:XY )->XY
      # Returns an XY containing the min X and the min Y of 'this' and 'other'.
      local min_x = which{ x<=other.x:x || other.x }
      local min_y = which{ y<=other.y:y || other.y }
      return XY( min_x, min_y )

    method normal->XY
      # Returns the normal of this vector.
      return XY( -y, x )

    method normalized->XY
      local msq = x*x + y*y
      if (msq <= 0) return XY(0,0)
      local m = msq.sqrt
      if (m) return XY( x/m, y/m )
      else   return XY.zero

    method operator?->Logical
      return x or y

    method operator==( other:XY )->Logical
      return x == other.x and y == other.y

    method operator-()->XY
      return XY( -x, -y )

    method operator<>( other:XY )->Int
      if (x == other.x) return y <> other.y
      else              return x <> other.x

    method or_larger( other:XY )->XY
      # Returns the largest X and the largest Y between this value and the other value.
      return XY( this.x.or_larger(other.x), this.y.or_larger(other.y) )

    method or_smaller( other:XY )->XY
      # Returns the smaller X and the smaller Y between this value and the other value.
      return XY( this.x.or_smaller(other.x), this.y.or_smaller(other.y) )

    method print_to( buffer:PrintWriter )
      buffer.print '('
      x.print_to( buffer )
      buffer.print ','
      y.print_to( buffer )
      buffer.print ')'

    method product->Real
      return x * y

    method radians->Radians
      return Radians( y.atan2(x) )

    method reflected( surface_normal:XY )->XY
      return this - 2 * (this.dot(surface_normal) * surface_normal)

    method rotated( angle:Degrees )->XY
      return XY( this.magnitude, radians + angle->Radians )

    method rotated( angle:Radians )->XY
      return XY( magnitude, radians + angle )

    method side( line:Line )->Int32
      # Returns 0 if on the line, 1 if on the same side as line.normal points out of,
      # and -1 if on the opposite side
      return line.direction.cross( this - line.a ).sign

    method sign->XY
      return XY( x.sign, y.sign )

    method sum->Real
      return x + y

    method to->String
      return "($,$)"(x,y)

    method xv->XY
      return XY(x,0)

    method yv->XY
      return XY(0,y)

    method yx->XY
      return XY(y,x)
endClass

augment Rogue::Random
  METHODS
    method xy( limit:Real )->Geometry::XY
      return Geometry::XY( real(limit), real(limit) )

    method xy( limit:Geometry::XY )->Geometry::XY
      return Geometry::XY( real(limit.x), real(limit.y) )
endAugment

class XY32( x:Real32, y:Real32 ) [compound]
  GLOBAL METHODS
    method create( xy:XY )->XY32
      return XY32( xy.x, xy.y )

  METHODS
    method hashcode->Int
      return x.hashcode ~ y.hashcode

    method operator?->Logical
      return x or y

    method operator<>( other:XY )->Int
      if (x == other.x) return y <> other.y
      else              return x <> other.x

    method to->XY
      return XY( x, y )
endClass
