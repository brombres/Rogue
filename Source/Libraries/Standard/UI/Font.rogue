library UI

class Font [abstract]
  PROPERTIES
    name         : String
    line_spacing = 1.0

  METHODS
    method init( name )

    method init( existing:Font )
      name = existing.name

    method cloned->$thisType
      return $thisType( this )

    method draw( text:Text ) [abstract]

    method draw( text:String, position:XY )  [abstract]

    method draw( text:String, position:XY, anchor:Anchor )
      if (anchor != Anchor.TOP_LEFT)
        position = Box(position,measure(text)).relative_to( anchor ).position
      endIf
      draw( text, position )

    method draw( text:String, bounds:Box, anchor=Anchor.CENTER:Anchor  )
      local text_size = measure( text )
      local position  = Box(text_size).positioned_within( bounds, anchor ).position
      draw( text, position )

    method draw( text:String, container:Component )
      draw( text, container.display_bounds, container.anchor )

    method height->Real                 [abstract]

    method measure( ch:Character )->XY  [abstract]

    method measure( text:String, i1=null:Int?, count=null:Int? )->XY
      if (not i1)    i1 = 0
      if (not count) count = text.count - i1.value

      block i1=i1.value, count=count.value
        local max_w = 0.0
        local w     = 0.0
        local h     = 0.0
        local row_h = 0.0

        forEach (i in i1..<i1+count)
          local ch = text[i]
          if (ch == '\n')
            max_w .= or_larger( w )
            w = 0
            row_h .= or_larger( measure(ch).y )
            h += row_h * line_spacing
            row_h = 0
          else
            local ch_size = measure(ch)
            w += ch_size.x
            row_h .= or_larger( ch_size.y )
          endIf
        endForEach

        max_w .= or_larger( w )
        h += row_h

        return XY( max_w, h )
      endBlock

endClass

