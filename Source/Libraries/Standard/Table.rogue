class Table<<$KeyType,$ValueType>>
  PROPERTIES
    count           : Int32
    bin_mask        : Int32
    cur_entry_index : Int32
    bins            : Array<<TableEntry<<$KeyType,$ValueType>>>>
    first_entry     : TableEntry<<$KeyType,$ValueType>>
    last_entry      : TableEntry<<$KeyType,$ValueType>>
    cur_entry       : TableEntry<<$KeyType,$ValueType>>

    sort_function   : (Function(TableEntry<<$KeyType,$ValueType>>,TableEntry<<$KeyType,$ValueType>>)->Logical)
    # Optional function which, if defined, is used to place each entry into the correct position
    # as entries are added or redefined.

  METHODS
    method init
      init( 16 )

    method init( bin_count:Int32 )
      local bins_power_of_2 = 1
      while (bins_power_of_2 < bin_count) bins_power_of_2 = bins_power_of_2 :<<: 1
      bin_count = bins_power_of_2
      bin_mask = bin_count - 1

      bins = Array<<TableEntry<<$KeyType,$ValueType>>>>( bin_count )

    method init( other:Table<<$KeyType,$ValueType>> )
      bin_mask = other.bin_mask
      ensure bins( other.bins.count )
      add( other )

    method add( other:Table<<$KeyType,$ValueType>> )->this
      local cur = other.first_entry
      while (cur)
        this[ cur.key ] = cur.value
        cur = cur.next_entry
      endWhile
      return this

    method at( index:Int32 )->$ValueType
      contingent
        necessary (count)

        if (not cur_entry)
          if (index <= count/2) cur_entry = first_entry; cur_entry_index = 0
          else                  cur_entry = last_entry;  cur_entry_index = count - 1
        endIf

        while (cur_entry_index < index)
          ++cur_entry_index
          cur_entry = cur_entry.next_entry
          necessary (cur_entry)
        endWhile

        while (cur_entry_index > index)
          --cur_entry_index
          cur_entry = cur_entry.previous_entry
          necessary (cur_entry)
        endWhile

        return cur_entry.value

      unsatisfied
        local default_value : $ValueType
        return default_value

      endContingent

    method clear
      forEach (i of bins) bins[i] = null
      first_entry = null
      last_entry = null
      cur_entry = null
      count = 0

    method cloned->Table<<$KeyType,$ValueType>>
      return Table<<$KeyType,$ValueType>>( this )

    method contains( key:$KeyType )->Logical
      return find(key)?

    method contains( query:(Function($ValueType)->Logical) )->Logical
      return first( query ).exists

    method count( query:(Function(Value)->Logical) )->Int32
      local result = 0
      local cur = first_entry
      while (cur)
        if (query(cur.value)) ++result
        cur = cur.next_entry
      endWhile
      return result

    method discard( query:(Function(TableEntry<<$KeyType,$ValueType>>)->Logical) )
      local discard_list : $KeyType[]
      local cur = first_entry
      while (cur)
        if (query(cur)) (ensure discard_list).add( cur.key )
        cur = cur.next_entry
      endWhile
      if (discard_list) remove( forEach in discard_list )

    method entries->TableEntriesIterator<<$KeyType,$ValueType>>
      # Returns an iterator compound.
      return TableEntriesIterator<<$KeyType,$ValueType>>( first_entry )

    method is_empty->Logical
      return (count == 0)

    method find( key:$KeyType )->TableEntry<<$KeyType,$ValueType>>
      local hash = key.hash_code
      local entry = bins[ hash & bin_mask ]

      while (entry)
        if (entry.hash == hash and entry.key == key) return entry
        entry = entry.adjacent_entry
      endWhile

      return null

    method first->$ValueType
      if (first_entry)
        return first_entry.value
      else
        local default_value : $ValueType
        return default_value
      endIf

    method first( query:(Function($ValueType)->Logical) )->$ValueType?
      local cur = first_entry
      while (cur)
        if (query(cur.value)) return cur.value
        cur = cur.next_entry
      endWhile
      return null

    method get( key:$KeyType )->$ValueType
      local entry = find( key )
      if (entry)
        return entry.value
      else
        local default_value : $ValueType
        return default_value
      endIf

    method get( key:$KeyType, default_value:$ValueType )->$ValueType
      local entry = find( key )
      if (entry)
        return entry.value
      else
        return default_value
      endIf

    method get( query:(Function($ValueType)->Logical) )->$ValueType[]
      local result = $ValueType[]
      local cur = first_entry
      while (cur)
        if (query(cur.value)) result.add( cur.value )
        cur = cur.next_entry
      endWhile
      return result

    method keys->TableKeysIterator<<$KeyType,$ValueType>>
      # Returns an iterator compound.
      return TableKeysIterator<<$KeyType,$ValueType>>( first_entry )

    method locate( query:(Function($ValueType)->Logical) )->$KeyType[]
      local result = $KeyType[]
      local cur = first_entry
      while (cur)
        if (query(cur.value)) result.add( cur.key )
        cur = cur.next_entry
      endWhile
      return result

    method operator==( other:Table<<$KeyType,$ValueType>> )->Logical
      if (count != other.count) return false

      local cur = first_entry
      while (cur)
        local other_entry = other.find( cur.key )
        if (not other_entry) return false
        if (other_entry.value != cur.value) return false
        cur = cur.next_entry
      endWhile

      return true

    method to_value_package->Value
      return this->Value

    method print_to( buffer:StringBuilder )->StringBuilder
      buffer.print( '{' )
      local cur = first_entry
      local i = 0
      while (cur)
        if (i > 0) buffer.print( ',' )
        buffer.print( cur.key )
        buffer.print( ':' )
        buffer.print( cur.value )
        cur = cur.next_entry
        ++i
      endWhile
      buffer.print( '}' )
      return buffer

    method random->TableEntry<<$KeyType,$ValueType>>
      if (count == 0) return null

      local r = Random.int32( count )
      local cur = first_entry
      while (cur)
        --r
        if (r < 0) return cur
        cur = cur.next_entry
      endWhile

      return first_entry  # shouldn't ever happen

    method remove( key:$KeyType )->$ValueType
      local entry = find( key )
      if (not entry)
        local default_zero_value : $ValueType
        return default_zero_value
      endIf
      remove( entry )
      return entry.value

    method remove( query:(Function($ValueType)->Logical) )->$ValueType[]
      # Returns the list of values that pass the query function while removing
      # them from this table.
      local result = $ValueType[]
      local cur = first_entry
      while (cur)
        local next_entry = cur.next_entry
        if (query(cur.value))
          result.add( cur.value )
          remove( cur )
        endIf
        cur = next_entry
      endWhile
      return result

    method remove( entry:TableEntry<<$KeyType,$ValueType>> )->TableEntry<<$KeyType,$ValueType>>
      assert (find(entry.key) is entry)
      --count
      cur_entry = null

      # Remove from bin
      local hash = entry.hash
      local index = hash & bin_mask
      local cur = bins[ index ]
      if (cur is entry)
        bins[ index ] = cur.adjacent_entry
      else
        while (cur.adjacent_entry is not entry) cur = cur.adjacent_entry
        cur.adjacent_entry = cur.adjacent_entry.adjacent_entry
      endIf

      _unlink( entry )

      return entry

    method remove_at( index:Int32 )->$ValueType
      if (index < 0 or index >= count)
        local zero_result : $ValueType
        return zero_result
      endIf

      local cur = first_entry
      loop (index) cur = cur.next_entry
      remove( cur )

      return cur.value

    method set( key:$KeyType, value:$ValueType )->this
      local entry = find( key )
      if (entry)
        entry.value = value
        if (sort_function) _adjust_entry_order( entry )
        return this
      endIf

      if (count >= bins.count) _grow()

      local hash = key.hash_code
      local index = hash & bin_mask

      # Add to the correct bin
      ensure entry( key, value, hash )
      entry.adjacent_entry = bins[ index ]
      bins[index] = entry

      _place_entry_in_order( entry )

      ++count
      return this

    method set_sort_function( @sort_function )->this
      return sort( sort_function )

    method sort( compare_fn:(Function(TableEntry<<$KeyType,$ValueType>>,TableEntry<<$KeyType,$ValueType>>)->Logical) )->this
      if (count <= 1) return this

      local list = entries.to_list
      list.sort( compare_fn )
      forEach (i in 0..<(list.count-1))
        list[i].next_entry       = list[i+1]
        list[i+1].previous_entry = list[i]
      endForEach
      first_entry = list.first
      last_entry = list.last
      first_entry.previous_entry = null
      last_entry.next_entry = null

      return this

    method sorted( compare_fn:(Function(TableEntry<<$KeyType,$ValueType>>,TableEntry<<$KeyType,$ValueType>>)->Logical) ) ...
        ->Table<<$KeyType,$ValueType>>
      return cloned.sort( compare_fn )

    method to->String
      return print_to( StringBuilder() )->String

    method to->Value
      local result = @{}
      local cur = first_entry
      while (cur)
        if (isReference(cur.value))
          if (cur.value) result[ cur.key->String ] = cur.value->Value
          else           result[ cur.key->String ] = NullValue
        else
          result[ cur.key->String ] = Value( cur.value )
        endIf
        cur = cur.next_entry
      endWhile
      return result

    method unpack( values:Value )
      init

      if (isString($KeyType) and not isAspect($ValueType))
        # Value tables only have string keys
        # Can't unpack aspect values because they're abstract types

        forEach (key in values.keys)
          this[ key ] = $ValueType.from_value( values[key] )
        endForEach
      endIf

    method values->TableValuesIterator<<$KeyType,$ValueType>>
      # Returns an iterator compound.
      return TableValuesIterator<<$KeyType,$ValueType>>( first_entry )

    method _adjust_entry_order( entry:TableEntry<<$KeyType,$ValueType>> )
      if (first_entry is last_entry) return # still in order, only one entry

      if (entry is first_entry)
        if (sort_function(entry,entry.next_entry)) return  # still in order
      elseIf (entry is last_entry)
        if (sort_function(entry.previous_entry,entry)) return  # still in order
      else
        if (sort_function(entry.previous_entry,entry) and sort_function(entry,entry.next_entry))
          return  # still in order
        endIf
      endIf

      # Not in order - unlink and then re-place
      _unlink( entry )
      _place_entry_in_order( entry )

    method _place_entry_in_order( entry:TableEntry<<$KeyType,$ValueType>> )
      if (first_entry)
        # Use the sort_function comparator if it exists
        if (sort_function)
          if (sort_function(entry,first_entry))
            # New entry comes before existing first entry
            entry.next_entry = first_entry
            first_entry.previous_entry = entry
            first_entry = entry
          elseIf (sort_function(last_entry,entry))
            # New entry goes after existing last entry
            last_entry.next_entry = entry
            entry.previous_entry = last_entry
            last_entry = entry
          else
            # New entry goes somewhere in the middle
            local cur = first_entry
            while (cur.next_entry)
              if (sort_function(entry,cur.next_entry))
                # Insert entry between cur and cur.next_entry
                entry.previous_entry = cur
                entry.next_entry = cur.next_entry
                entry.next_entry.previous_entry = entry
                cur.next_entry = entry
                escapeWhile
              endIf
              cur = cur.next_entry
            endWhile
          endIf
        else
          # No sort function; place at the end of the order list
          last_entry.next_entry = entry
          entry.previous_entry = last_entry
          last_entry = entry
        endIf
      else
        # This is the first entry
        first_entry = entry
        last_entry = entry
      endIf

    method _unlink( entry:TableEntry<<$KeyType,$ValueType>> )
      if (entry is first_entry)
        if (entry is last_entry)
          first_entry = null
          last_entry = null
        else
          first_entry = entry.next_entry
          first_entry.previous_entry = null
        endIf

      elseIf (entry is last_entry)
        last_entry = entry.previous_entry
        last_entry.next_entry = null

      else
        entry.previous_entry.next_entry = entry.next_entry
        entry.next_entry.previous_entry = entry.previous_entry

      endIf

    method _grow
      # Grow by a factor of two
      bins = Array<<TableEntry<<$KeyType,$ValueType>>>>( bins.count * 2 )
      bin_mask = (bin_mask :<<: 1) | 1

      local cur = first_entry
      while (cur)
        local index = cur.hash & bin_mask
        cur.adjacent_entry = bins[index]
        bins[index] = cur
        cur = cur.next_entry
      endWhile

endClass

class TableEntry<<$KeyType,$ValueType>>
  PROPERTIES
    key               : $KeyType
    value             : $ValueType
    adjacent_entry    : TableEntry<<$KeyType,$ValueType>>  # next entry in bin - same hash code after masking
    next_entry        : TableEntry<<$KeyType,$ValueType>>  # next entry in order - could be totally different hash
    previous_entry    : TableEntry<<$KeyType,$ValueType>>  # previous entry in order
    hash              : Int32

  METHODS
    method init( _key:$KeyType, _value:$ValueType, _hash:Int32 )
      key = _key
      value = _value
      hash = _hash

    method to->String
      return "($:$)" (key, value)

    # Support tuple-like protocol for destructuring assignment
    method _1->$KeyType
      return key

    method _2->$ValueType
      return value
endClass

class TableEntriesIterator<<$KeyType,$ValueType>>( cur:TableEntry<<$KeyType,$ValueType>> ) [compound]
  METHODS
    method has_another->Logical
      return cur?

    method peek->TableEntry<<$KeyType,$ValueType>>
      return cur

    method peek( lookahead:Int32 )->TableEntry<<$KeyType,$ValueType>>
      block cur
        loop (lookahead)
          if (not cur) return null
          cur = cur.next_entry
        endLoop
        return cur
      endBlock

    method read->TableEntry<<$KeyType,$ValueType>> [mutating]
      local result = cur
      cur = cur.next_entry
      return result

    method read_another->TableEntry<<$KeyType,$ValueType>>? [mutating]
      if (not cur) return null
      local result = cur
      cur = cur.next_entry
      return result

    method to->TableEntry<<$KeyType,$ValueType>>[]( result=null:TableEntry<<$KeyType,$ValueType>>[] )
      return to_list( result )

    method to_list( result=null:TableEntry<<$KeyType,$ValueType>>[] )->TableEntry<<$KeyType,$ValueType>>[]
      ensure result
      result.add( forEach in this )
      return result
endClass

class TableKeysIterator<<$KeyType,$ValueType>>( cur:TableEntry<<$KeyType,$ValueType>> ) [compound]
  METHODS
    method has_another->Logical
      return cur?

    method peek->$KeyType
      return cur.key

    method peek( lookahead:Int32 )->$KeyType
      block cur
        loop (lookahead)
          if (not cur) escapeBlock
          cur = cur.next_entry
        endLoop
        if (cur) return cur.key
      endBlock
      local default : $KeyType
      return default

    method read->$KeyType [mutating]
      local result = cur.key
      cur = cur.next_entry
      return result

    method read_another->$KeyType? [mutating]
      if (not cur) return null
      local result = cur.key
      cur = cur.next_entry
      return result

    method to->$KeyType[]( result=null:$KeyType[] )
      return to_list( result )

    method to_list( result=null:$KeyType[] )->$KeyType[]
      ensure result
      result.add( forEach in this )
      return result
endClass

class TableValuesIterator<<$KeyType,$ValueType>>( cur:TableEntry<<$KeyType,$ValueType>> ) [compound]
  METHODS
    method has_another->Logical
      return cur?

    method peek->$ValueType
      return cur.value

    method peek( lookahead:Int32 )->$ValueType
      block cur
        loop (lookahead)
          if (not cur) escapeBlock
          cur = cur.next_entry
        endLoop
        if (cur) return cur.value
      endBlock
      local default : $ValueType
      return default

    method read->$ValueType [mutating]
      local result = cur.value
      cur = cur.next_entry
      return result

    method read_another->$ValueType? [mutating]
      if (not cur) return null
      local result = cur.value
      cur = cur.next_entry
      return result

    method to->$ValueType[]( result=null:$ValueType[] )
      return to_list( result )

    method to_list( result=null:$ValueType[] )->$ValueType[]
      ensure result
      result.add( forEach in this )
      return result
endClass

class StringTable<<$ValueType>> : Table<<String,$ValueType>>
  # Adds support for StringBuilder and Character[] keys.
  METHODS
    method contains( key:StringBuilder )->Logical
      return find_key( key )?

    method contains( key:Character[] )->Logical
      return find_key( key )?

    method find( key:StringBuilder )->TableEntry<<String,$ValueType>>
      local key_string = find_key( key )
      if (not key_string) return null
      return find( key_string )

    method find( key:Character[] )->TableEntry<<String,$ValueType>>
      local key_string = find_key( key )
      if (not key_string) return null
      return find( key_string )

    method find_key( key:StringBuilder )->String
      local len  = key.count
      local hash = key.hash_code

      local cur = first_entry
      while (cur)
        local k = cur.key
        if (k.hash_code == hash and k.count == len and k == key) return k
        cur = cur.next_entry
      endWhile

      return null

    method find_key( key:Character[] )->String
      local len  = key.count
      local hash = 0
      forEach (ch in key)
        hash = ((hash:<<:3) - hash) + ch
      endForEach

      local cur = first_entry
      while (cur)
        local k = cur.key
        if (k.hash_code == hash and k.count == len)
          contingent
            forEach (ch at index in key)
              necessary (ch == k[index])
            endForEach
            return k
          endContingent
        endIf
        cur = cur.next_entry
      endWhile

      return null

    method get( key:StringBuilder )->$ValueType
      local key_string = find_key( key )
      if (not key_string)
        local default_value : $ValueType
        return default_value
      endIf
      return get( key_string )

    method get( key:Character[] )->$ValueType
      local key_string = find_key( key )
      if (not key_string)
        local default_value : $ValueType
        return default_value
      endIf
      return get( key_string )

    method remove( key:StringBuilder )->$ValueType
      local key_string = find_key( key )
      if (not key_string)
        local default_value : $ValueType
        return default_value
      endIf
      return remove( key_string )

    method remove( key:Character[] )->$ValueType
      local key_string = find_key( key )
      if (not key_string)
        local default_value : $ValueType
        return default_value
      endIf
      return remove( key_string )

    method set( key:StringBuilder, value:$ValueType )->this
      local key_string = find_key( key )
      if (not key_string) key_string = key->String
      set( key_string, value )
      return this

    method set( key:Character[], value:$ValueType )->this
      local key_string = find_key( key )
      if (not key_string) key_string = String( key )
      set( key_string, value )
      return this

endClass

