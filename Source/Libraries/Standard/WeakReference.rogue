class WeakReference [requisite]
  PROPERTIES
    next_weak_reference : WeakReference
    native "RogueObject* value;"

  METHODS
    method init( _value:Object )
      native @|$this->value = $_value;
              |$next_weak_reference = Rogue_weak_references;
              |Rogue_weak_references = $this;

    method clean_up
      native @|if (Rogue_weak_references == $this)
              |{
              |  Rogue_weak_references = $next_weak_reference;
              |}
              |else
              |{
              |  RogueWeakReference* cur = Rogue_weak_references;
              |  while (cur && cur->next_weak_reference != $this)
              |  {
              |    cur = cur->next_weak_reference;
              |  }
              |  if (cur) cur->next_weak_reference = cur->next_weak_reference->next_weak_reference;
              |}

    method set_value( new_value:Object )->this
      native @|$this->value = $new_value;
      return this

    method value->Object
      return native( "$this->value" )->Object
endClass

class WeakReference<<$DataType>> : WeakReference
  METHODS
    method value->$DataType
      return native( "$this->value" )->$DataType
endClass

