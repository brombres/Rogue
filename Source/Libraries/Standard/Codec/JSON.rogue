# This library is included by RogueC as needed.

class JSON : String
  GLOBAL METHODS
    method load( file:File, error_log=null:PrintWriter )->Variant
      if (not file.exists) return undefined
      return parse( String(file) )

    method parse( file:File, error_log=null:PrintWriter )->Variant
      return load( file, &=error_log )

    method parse( json:String, error_log=null:PrintWriter )->Variant
      try
        return JSONParser( json ).parse_value
      catch (err:JSONParseError)
        if (error_log) error_log.println "JSON parse error: " + err
      endTry

    method save( value:Variant, file:File, &formatted, &omit_commas )->Logical
      return file.save( value.to_json(&=formatted, &=omit_commas)  )

  METHODS
    method parse( error_log=null:PrintWriter )->Variant
      return parse( this, error_log )

endClass

class JSONParseError( message ) : Error;

class JSONParser
  PROPERTIES
    reader : Scanner

  METHODS
    method init( json:String )
      reader = Scanner( json )

    method init( file:File )
      init( String(file) )

    method init( reader )

    method consume( ch:Character )->Logical
      return reader.consume( ch )

    method has_another->Logical
      return reader.has_another

    method next_is( ch:Character )->Logical
      return (reader.peek == ch)

    method parse_value->Variant
      consume_whitespace_and_eols

      if (not reader.has_another) return undefined

      local ch = reader.peek
      if (ch == '{') return parse_table
      if (ch == '[') return parse_list

      if (ch == '-')               return parse_number
      if (ch >= '0' and ch <= '9') return parse_number

      if (ch == '"' or ch == '\'')
        local result = parse_string
        if (result.count == 0) return ""

        local first_ch = result[ 0 ]
        if (first_ch == 't' and result == "true")  return true
        if (first_ch == 'f' and result == "false") return false
        if (first_ch == 'n' and result == "null")  return null

        return result

      elseIf (next_is_identifier)
        local result = parse_identifier
        if (result.count == 0) return ""

        local first_ch = result[ 0 ]
        if (first_ch == 't' and result == "true")  return true
        if (first_ch == 'f' and result == "false") return false
        if (first_ch == 'n' and result == "null")  return null

        return result
      else
        return undefined
      endIf

    method parse_table( open_ch='{':Character, close_ch='}':Character )->Variant
      consume_whitespace_and_eols

      if (not reader.consume(open_ch)) return undefined

      consume_whitespace_and_eols

      local table = @{}
      if (reader.consume(close_ch)) return table

      local prev_pos = reader.position
      local first = true
      while (first or reader.consume(',') or (reader.has_another and reader.peek!=close_ch and reader.position>prev_pos))
        first = false
        prev_pos = reader.position

        consume_whitespace_and_eols

        if (next_is_identifier)
          local key = parse_identifier
          consume_whitespace_and_eols

          if (key.count)
            if (reader.consume(':'))
              consume_whitespace_and_eols
              local value = parse_value
              table.set( key, value )
            else
              table.set( key, true )
            endIf
            consume_whitespace_and_eols
          endIf
        endIf
      endWhile

      if (not reader.consume(close_ch)) throw JSONParseError( "'$' expected."(close_ch) )

      return table

    method parse_list( open_ch='[':Character, close_ch=']':Character )->Variant
      consume_whitespace_and_eols

      if (not reader.consume(open_ch)) return undefined

      consume_whitespace_and_eols

      local list = @[]
      if (reader.consume(close_ch)) return list

      local prev_pos = reader.position
      local first = true
      while (first or reader.consume(',') or (reader.has_another and reader.peek!=close_ch and reader.position>prev_pos))
        first = false
        prev_pos = reader.position
        consume_whitespace_and_eols
        if (reader.peek == close_ch) escapeWhile
        list.add( parse_value )
        consume_whitespace_and_eols
      endWhile

      if (not reader.consume(close_ch)) throw JSONParseError( "'$' expected." (close_ch) )

      return list

    method parse_string->String
      consume_whitespace_and_eols

      local terminator = '"'
      if     (reader.consume( '"' ))  terminator = '"'
      elseIf (reader.consume( '\'' )) terminator = '\''

      if (not reader.has_another) return ""

      local buffer = String()
      local ch = reader.read
      while (reader.has_another and ch != terminator)
        if (ch == '\\')
          ch = reader.read
          if (ch == 'b')     buffer.print( '\b' )
          elseIf (ch == 'f') buffer.print( '\f' )
          elseIf (ch == 'n') buffer.print( '\n' )
          elseIf (ch == 'r') buffer.print( '\r' )
          elseIf (ch == 't') buffer.print( '\t' )
          elseIf (ch == 'u') buffer.print( parse_hex_quad )
          else               buffer.print( ch )
        else
          buffer.print( ch )
        endIf
        ch = reader.read
      endWhile

      return buffer

    method parse_hex_quad->Character
      local code = 0
      loop 4
        if (reader.has_another)
          local n = reader.read.to_number(16)
          code = (code :<<: 4) | which{n>=0:n || 0}
        endIf
      endLoop
      return code->Character

    method parse_identifier->String
      consume_whitespace_and_eols

      local ch = reader.peek
      if (ch == '"' or ch == '\'')
        return parse_string
      else
        local buffer = String()
        local finished = false
        while (not finished and reader.has_another)
          if (ch.is_identifier("$"))
            reader.read
            buffer.print( ch->Character )
            ch = reader.peek
          else
            finished = true
          endIf
        endWhile

        if (buffer.count == 0) throw JSONParseError( "Identifier expected." )
        return buffer
      endIf

    method peek->Character
      return reader.peek

    method next_is_identifier->Logical
      local ch = reader.peek
      if (ch.is_identifier_start("$")) return true
      return (ch == '"' or ch == '\'')

    method parse_number->Variant
      consume_whitespace_and_eols

      local sign = 1
      if (reader.consume( '-' ))
        sign = -1
        consume_whitespace_and_eols
      endIf

      local int_n = 0
      local real_n = 0.0
      local ch = reader.peek
      local digits = 0
      while (reader.has_another and ch >= '0' and ch <= '9')
        reader.read
        int_n = int_n * 10 + (ch - '0')
        real_n = real_n * 10 + (ch - '0')
        ch = reader.peek
        ++digits
      endWhile

      local is_real = (digits > 19)

      if (reader.consume( '.' ))
        is_real = true
        local decimal = 0.0
        local power = 0.0
        ch = reader.peek
        while (reader.has_another and ch >= '0' and ch <= '9')
          reader.read
          decimal = decimal * 10 + (ch - '0')
          power += 1.0
          ch = reader.peek
        endWhile
        real_n += decimal / (10.0 ^ power)
      endIf

      if (reader.consume( 'e' ) or reader.consume( 'E' ))
        is_real = true
        local negexp = false
        if (not reader.consume('+') and reader.consume('-')) negexp = true

        local power = 0.0
        ch = reader.peek
        while (reader.has_another and ch >= '0' and ch <= '9')
          reader.read
          power = power * 10.0 + (ch - '0')
          ch = reader.peek
        endWhile

        if (negexp) real_n /= 10 ^ power
        else        real_n *= 10 ^ power
      endIf

      if (is_real)
        return real_n * sign
      else
        return int_n * sign
      endIf

    method read->Character
      return reader.read

    method consume_spaces
      while (reader.consume_spaces) noAction

    method consume_whitespace
      while (reader.consume_whitespace) noAction

    method consume_whitespace_and_eols
      while (reader.consume_whitespace or reader.consume_eols) noAction

endClass

