uses IO/DataIO

class UTF16String : Poolable
  PROPERTIES
    data = Byte[]

  METHODS
    method init
      data.clear

    method init( text:String )
      data.clear

      local utf16_count : Int
      forEach (ch in text)
        if (ch >= 0x10_000)
          utf16_count += 2
        else
          ++utf16_count
        endIf
      endForEach

      data.reserve( (utf16_count+1) * 2 )
      forEach (ch in text)
        if (ch >= 0x1_0000)
          local u10 = ch - 0x1_0000
          local w1 = 0xD800 | ((u10:>>>:10) & 0x3FF)
          local w2 = 0xDC00 | (u10 & 0x3FF)
          data.add( w1 & 255 )
          data.add( (w1:>>>:8) & 255 )
          data.add( w2 & 255 )
          data.add( (w2:>>>:8) & 255 )
        else
          data.add( ch & 255 )
          data.add( (ch:>>>:8) & 255 )
        endIf
      endForEach

      data.[ add(0), add(0) ]  # null terminator

    method init( utf16_bytes:Byte[] )
      data.clear
      data.reserve( utf16_bytes.count )
      data.add( utf16_bytes )

    method init( wchar_data:RogueWordPointer )
      data.clear
      local count = -1
      while (native("$wchar_data.value[++$count]")->Int32) noAction
      ++count  # include null terminator
      data.expand_to_count( count*2 )
      native @|memcpy( $data->as_bytes, $wchar_data.value, $count*2 );

    method on_return_to_pool
      data.clear

    method to->String
      local n = data.count
      if (n <= 2) return ""
      while (n >= 2 and data[n-2]==0 and data[n-1]==0)
        # Latest version of Windows adds on multiple null terminators in GetFullPathNameW(); strip them all out
        data.remove_last
        data.remove_last
        n = data.count
      endWhile
      data.[ add(0), add(0) ]  # add single null terminator back on for processing

      use builder = StringPool
        local reader = DataReader( data )
        while (reader.has_another)
          local w1 = reader.read_int16_low_high
          if (w1 >= 0xD800 and w1 <= 0xDBFF)
            local rewind_pos = reader.position
            local w2 = reader.read_int16_low_high
            if (w2 >= 0xDC00 and w2 <= 0xDFFF)
              local ch = Character( (((w1 & 0x3FF) :<<: 10) | (w2 & 0x3FF)) + 0x1_0000 )
              builder.print( ch )
            else
              # w1 is an illegal unpaired leading surrogate. Treat it as a single code point
              # and save the following int16 for the next iteration.
              reader.seek( rewind_pos )
              builder.print( w1->Character )
            endIf
          else
            builder.print( w1->Character )
          endIf
        endWhile
        builder.remove_last  # null terminator
        return builder.cloned
      endUse

endClass
