class System [essential]
  DEPENDENCIES
    nativeHeader
      #if __APPLE__
        #include "TargetConditionals.h"
      #endif
    endNativeHeader

  GLOBAL PROPERTIES
    command_line_arguments = String[]
    executable_filepath    : String
    environment            : SystemEnvironment

  GLOBAL METHODS
    method exit( result_code=0:Int32 )
      $if (target("C++"))
        native @|Rogue_quit();
                |exit( $result_code );
      $endIf

    method home_folder->String
      which (System.os)
        case "Windows": return System.environment//HOMEDRIVE + System.environment//HOMEPATH
        others:         return System.environment//HOME
      endWhich

    method os->String
      # Returns one of:
      #   macOS
      #   Windows
      #   iOS
      #   Android
      #   Cygwin
      #   emscripten
      #   Linux (includes Unix and Posix)
      local result = "Linux"  # default
      native @|#if __APPLE__
              |  #if TARGET_IPHONE_SIMULATOR || TARGET_OS_IPHONE
                   result = "iOS"
      native @|  #else
                   result = "macOS"
      native @|  #endif
              |#elif _WIN64 || _WIN32
                 result = "Windows"
      native @|#elif ANDROID
                 result = "Android"
      native @|#elif __CYGWIN__
                 result = "Cygwin"
      native @|#elif defined(EMSCRIPTEN)
                 result = "emscripten"
      native @|#endif
      return result


    method run( command:String )->Int32
      # TODO: http://stackoverflow.com/questions/27046728/how-do-you-use-posix-spawn-to-replace-the-deprecated-system-to-launch-opendiff
      local return_val = 0
      native @|$return_val = system( (char*)$command->utf8 );
      if (return_val == -1) return -1
      native @|#if !defined(ROGUE_PLATFORM_WINDOWS)
              |$return_val = (RogueInt32) WEXITSTATUS($return_val);
              |#endif
      return return_val

    method sleep( seconds:Real64 )
      # Suspends execution of this program for the specified number of seconds.
      local do_exit = seconds > 0.5 # Or should it be lower? Used for multithreaded synchronization.

      native @|#ifdef ROGUE_PLATFORM_WINDOWS
               local ms = (seconds * 1000)->Int32
      native @|if ($do_exit) ROGUE_EXIT;
              |Sleep( $ms );
              |if ($do_exit) ROGUE_ENTER;
              |#else
               local nanoseconds = Int32( seconds.fractional_part * 1000000000.0 )
               seconds = seconds.whole_part
      native @|timespec sleep_time;
              |sleep_time.tv_sec = (time_t) $seconds;
              |sleep_time.tv_nsec = (long) $nanoseconds;
              |if ($do_exit) ROGUE_EXIT;
              |nanosleep( &sleep_time, NULL );
              |if ($do_exit) ROGUE_ENTER;
              |#endif

    method sync_storage
      noAction

    method time->Real64
      $if (target("C++"))
        native @|#if defined(_WIN32)
                |  struct __timeb64 time_struct;
                |  RogueReal64 time_seconds;
                |  _ftime64_s( &time_struct );
                |  time_seconds = (RogueReal64) time_struct.time;
                |  time_seconds += time_struct.millitm / 1000.0;
                |  return time_seconds;
                |
                |#else
                |  struct timeval time_struct;
                |  RogueReal64 time_seconds;
                |  gettimeofday( &time_struct, 0 );
                |  time_seconds = (RogueReal64) time_struct.tv_sec;
                |  time_seconds += (time_struct.tv_usec / 1000000.0);
                |  return time_seconds;
                |#endif
      $endIf
endClass

class SystemEnvironment [compound]
  METHODS
    method contains( name:String )->Logical
      local result : Logical
      native @|char* c_result = getenv( (char*)$name->utf8 );
              |if (c_result)
              |{
              |  $result = true;
              |}
      return result

    method get( name:String )->String
      local result : String

      native @|char* c_result = getenv( (char*)$name->utf8 );
              |if (c_result)
              |{
              |  $result = RogueString_create_from_utf8( c_result );
              |}

      return select{ result:result || null }

    method listing->String[]
      nativeHeader extern "C" char **environ;

      local result = String[]
      native "char** env = environ;"
      while (native("*env")->Logical)
        result.add( native("RogueString_create_from_utf8( *(env++) )")->String.before_first('=') )
      endWhile

      return result;

    method set( name:String, value:String )
      if (value and value.count)
        native @|setenv( (char*)$name->utf8, (char*)$value->utf8, 1 );
      else
        native @|unsetenv( (char*)$name->utf8 );
      endIf
endClass
