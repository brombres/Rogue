class TypeInfo
  ENUMERATE
    TYPE_NA
    TYPE_REAL64
    TYPE_REAL32
    TYPE_INT64
    TYPE_INT32
    TYPE_CHARACTER
    TYPE_BYTE
    TYPE_LOGICAL

  DEPENDENCIES
    nativeHeader
      struct RogueType;
      struct RogueTypeInfo;
      RogueTypeInfo* RogueType_type_info( RogueType* THIS );
    endNativeHeader

    nativeCode
      RogueTypeInfo* RogueType_type_info( RogueType* THIS )
      {
        if ( !THIS->type_info )
        {
          THIS->type_info = RogueTypeInfo__init__Int32_String( (RogueTypeInfo*)ROGUE_CREATE_OBJECT(TypeInfo),
              THIS->index, Rogue_literal_strings[ THIS->name_index ] );

          for (int i=0; i<THIS->global_property_count; ++i)
          {
            RogueTypeInfo__add_global_property_info__Int32_Int32( (RogueTypeInfo*) THIS->type_info,
                THIS->global_property_name_indices[i], THIS->global_property_type_indices[i] );
          }

          for (int i=0; i<THIS->property_count; ++i)
          {
            RogueTypeInfo__add_property_info__Int32_Int32( (RogueTypeInfo*) THIS->type_info,
                THIS->property_name_indices[i], THIS->property_type_indices[i] );
          }

          for (int i=0; i<THIS->method_count; ++i)
          {
            RogueTypeInfo__add_method_info__Int32( (RogueTypeInfo*) THIS->type_info,
              (RogueInt32)(THIS->methods - Rogue_dynamic_method_table + i) );
          }

          for (int i=0; i<THIS->global_method_count; ++i)
          {
            RogueTypeInfo__add_global_method_info__Int32( (RogueTypeInfo*) THIS->type_info,
              (RogueInt32)(THIS->methods - Rogue_dynamic_method_table + THIS->method_count + i) );
          }
        }

        return (RogueTypeInfo*) THIS->type_info;
      }
    endNativeCode

  GLOBAL METHODS
    method get ( index : Int32 ) -> TypeInfo
      if ((index < 0) or (index >= type_count)) return null
      return native("RogueType_type_info(&Rogue_types[$index])")->TypeInfo

    method get ( name : String ) -> TypeInfo
      local result = RuntimeTypeInfoLookup[ name ]
      if (result) return result
      return _create_type_info( name )

    method get ( name : StringBuilder ) -> TypeInfo
      local result = RuntimeTypeInfoLookup[ name ]
      if (result) return result
      return _create_type_info( name->String )

    method _create_type_info( name:String )->TypeInfo
      local result : TypeInfo

      native @|for (int i=Rogue_type_count; --i>=0; )
              |{
              |  RogueType* cur_type = &Rogue_types[ i ];
              |  if (RogueType_name_equals( cur_type, (char*) $name->utf8 ))
              |  {
              |    $result = RogueType_type_info( cur_type );
              |    break;
              |  }
              |}

      if (result) RuntimeTypeInfoLookup[ name ] = result
      return result

    method type_count -> Int32
      return native("Rogue_type_count")->Int32

  PROPERTIES
    index             : Int32
    name              : String
    global_properties = PropertyInfo[]
    properties        = PropertyInfo[]
    global_methods    = MethodInfo[]
    methods           = MethodInfo[]
    primitive_type    = TYPE_NA : Int32

  METHODS
    method init( index, name ) [essential]
      which (name)
        case "Real64"
          primitive_type = TYPE_REAL64
        case "Real32"
          primitive_type = TYPE_REAL32
        case "Int64"
          primitive_type = TYPE_INT64
        case "Int32"
          primitive_type = TYPE_INT32
        case "Character"
          primitive_type = TYPE_CHARACTER
        case "Byte"
          primitive_type = TYPE_BYTE
        case "Logical"
          primitive_type = TYPE_LOGICAL
      endWhich

    method add_global_property_info( global_property_name_index:Int32, global_property_type_index:Int32 ) [essential]
      global_properties.add( PropertyInfo(global_properties.count,global_property_name_index, global_property_type_index) )

    method add_property_info( property_name_index:Int32, property_type_index:Int32 ) [essential]
      properties.add( PropertyInfo(properties.count,property_name_index, property_type_index) )

    method add_method_info( method_index:Int32 ) [essential]
      local m = create_method( method_index )
      if (m) methods.add( m )

    method add_global_method_info( method_index:Int32 ) [essential]
      local m = create_method( method_index );
      if (m) global_methods.add( m )

    method create_method( method_index:Int32 )->MethodInfo
      local info_index = native("Rogue_method_info_index[$method_index]")->Int32
      local first_param_name  : Int32
      local first_param_type  : Int32
      local parameter_count  : Int32
      local return_type  : Int32
      local call_handler : Int32
      local success = false
      native @|auto info = Rogue_method_info_table[ $info_index ];
              |if (info[0] >= 0)
              |{
              |  $success = 1;
              |  $parameter_count  = info[1];
              |  $first_param_name = info[2];
              |  $first_param_type = info[3];
              |  $return_type      = info[4];
              |  $call_handler     = info[5];
              |}
      if (success)
        return MethodInfo( method_index, parameter_count, first_param_name, first_param_type,
                           return_type, call_handler)
      else
        return null
      endIf

    method call( name_or_sig:String, args=UndefinedValue:Value )->Value
      # - Global call syntax
      # - Can throw NoSuchMethodError
      local m = find_global_method( name_or_sig, args )
      if (not m)
        if (name_or_sig.contains('('))
          throw NoSuchMethodError( "$.$" (name,name_or_sig) )
        else
          throw NoSuchMethodError( "$.$($)" (name,name_or_sig,args.count) )
        endIf
      endIf

      return call( m, args )

    method call( m:MethodInfo, args=UndefinedValue:Value )->Value
      # - Global call syntax
      return m.call( args )

    method call( context:Object, name_or_sig:String, args=UndefinedValue:Value )->Value
      # Note: can throw NoSuchMethodError
      local m = find_method( name_or_sig, args )
      if (not m)
        if (name_or_sig.contains('('))
          throw NoSuchMethodError( "$.$" (name,name_or_sig) )
        else
          throw NoSuchMethodError( "$.$($)" (name,name_or_sig,args.count) )
        endIf
      endIf

      return call( context, m, args )

    method call( context:Object, m:MethodInfo, args=UndefinedValue:Value )->Value
      if (not args.is_list) args = @[]

      args.insert( Value(context) )
      local result = m.call( args )
      args.remove_first
      return result

    method create_object->Object
      if (is_primitive or is_compound)
        local boxed_name = "Boxed<<$>>" (name)
        local boxed_type = TypeInfo[ boxed_name ]
        if (boxed_type) return boxed_type.create_object
        else            throw InstanceError( "Error creating primitive or compound object: cannot find type $." (boxed_name) )
      endIf

      if (is_aspect)
        throw InstanceError( "Cannot create an object from an aspect type - aspects are abstract." )
      endIf

      native @|return RogueType_create_object( &Rogue_types[$index], 0 );

    method create_object( initial_values:Value )->Object
      local obj = create_object
      obj.unpack( initial_values )
      return obj

    method create_object<<$AsType>>->$AsType
      return create_object as $AsType

    method global_property( property_name:String )->Value
      return global_property( find_global_property(property_name) )

    method global_property( property_info:PropertyInfo )->Value
      if (not property_info) return UndefinedValue

      local i = property_info.index

      if (property_info.type.is_reference)
        return Value( native( "*((RogueObject**)(Rogue_types[$index].global_property_pointers[$i]))" )->Object )
      endIf

      which (property_info.type.primitive_type)
        case TYPE_REAL64
          return Value( native( "*((RogueReal64*)(Rogue_types[$index].global_property_pointers[$i]))" )->Real64 )

        case TYPE_REAL32
          return Value( native( "*((RogueReal32*)(Rogue_types[$index].global_property_pointers[$i]))" )->Real32 )

        case TYPE_INT64
          return Value( native( "*((RogueInt64*)(Rogue_types[$index].global_property_pointers[$i]))" )->Int64 )

        case TYPE_INT32
          return Value( native( "*((RogueInt32*)(Rogue_types[$index].global_property_pointers[$i]))" )->Int32 )

        case TYPE_CHARACTER
          return Value( native( "*((RogueCharacter*)(Rogue_types[$index].global_property_pointers[$i]))" )->Character + "" )

        case TYPE_BYTE
          return Value( native( "*((RogueByte*)(Rogue_types[$index].global_property_pointers[$i]))" )->Byte )

        case TYPE_LOGICAL
          return Value( native( "*((RogueLogical*)(Rogue_types[$index].global_property_pointers[$i]))" )->Logical )

        others
          # Must be a compound
          return Introspector( null, native("(RogueInt64)(intptr_t)(Rogue_types[$index].global_property_pointers[$i])")->Int64,
              property_info.type )->Value
      endWhich

    method global_property<<$PropertyType>>( property_name:String )->$PropertyType
      return global_property<<$PropertyType>>( find_global_property(property_name) )

    method global_property<<$PropertyType>>( property_info:PropertyInfo )->$PropertyType
      if (not property_info)
        local default_value : $PropertyType
        return default_value
      endIf

      local i = property_info.index
      if (isReference($PropertyType))
        return native( "*((RogueObject**)(Rogue_types[$index].global_property_pointers[$i]))" )->Object as $PropertyType
      else
        return native( "*(($($PropertyType)*)(Rogue_types[$index].global_property_pointers[$i]))" )->$PropertyType
      endIf

    method global_property_count->Int32
      # Convenience method to provide symmetric interface with per-object `property_count` methods.
      return global_properties.count

    method global_property_name( global_property_index:Int32 )->String
      # Convenience method to provide symmetric interface with per-object `property_name` methods.
      return global_properties[ global_property_index ].name

    method find_global_property( property_name:String )->PropertyInfo
      forEach (i in 0..<global_property_count)
        if (global_properties[i].name == property_name)
          return global_properties[ i ]
        endIf
      endForEach
      return null

    method instance_of( other:TypeInfo )->Logical
      return native( "RogueType_instance_of( &Rogue_types[$index], &Rogue_types[$other->index] )" )->Logical

    method is_aspect->Logical
      return native( "(Rogue_types[$index].attributes & ROGUE_ATTRIBUTE_TYPE_MASK) == ROGUE_ATTRIBUTE_IS_ASPECT" )->Logical

    method is_class->Logical
      return native( "(Rogue_types[$index].attributes & ROGUE_ATTRIBUTE_TYPE_MASK) == ROGUE_ATTRIBUTE_IS_CLASS" )->Logical

    method is_compound->Logical
      return native( "(Rogue_types[$index].attributes & ROGUE_ATTRIBUTE_TYPE_MASK) == ROGUE_ATTRIBUTE_IS_COMPOUND" )->Logical

    method is_direct->Logical
      return native( "Rogue_types[$index].attributes & ROGUE_ATTRIBUTE_IS_DIRECT" )->Logical

    method is_primitive->Logical
      return native( "(Rogue_types[$index].attributes & ROGUE_ATTRIBUTE_TYPE_MASK) == ROGUE_ATTRIBUTE_IS_PRIMITIVE" )->Logical

    method is_real64->Logical
      return (primitive_type == TYPE_REAL64)

    method is_real32->Logical
      return (primitive_type == TYPE_REAL32)

    method is_int64->Logical
      return (primitive_type == TYPE_INT64)

    method is_int32->Logical
      return (primitive_type == TYPE_INT32)

    method is_character->Logical
      return (primitive_type == TYPE_CHARACTER)

    method is_byte->Logical
      return (primitive_type == TYPE_BYTE)

    method is_logical->Logical
      return (primitive_type == TYPE_LOGICAL)

    method is_reference->Logical
      return native( "!(Rogue_types[$index].attributes & ROGUE_ATTRIBUTE_IS_DIRECT)" )->Logical

    method is_singleton->Logical
      return native( "(Rogue_types[$index].attributes & ROGUE_ATTRIBUTE_IS_SINGLETON)" )->Logical

    method base_class->TypeInfo
      local base_count : Int32
      native "$base_count = Rogue_types[$index].base_type_count;"
      forEach (i in 0..<base_count)
        local ti : TypeInfo
        native @|RogueType * base = Rogue_types[$this->index].base_types[$i];
                |RogueType_type_info(base);
                |$ti = (RogueTypeInfo*)base->type_info;
        if (not ti.is_aspect) return ti
      endForEach
      return null

    method aspects->TypeInfo[]
      local r = TypeInfo[]
      local base_count : Int32
      native "$base_count = Rogue_types[$index].base_type_count;"
      forEach (i in 0..<base_count)
        local ti : TypeInfo
        native @|RogueType * base = Rogue_types[$this->index].base_types[$i];
                |RogueType_type_info(base);
                |$ti = (RogueTypeInfo*)base->type_info;
        if (ti.is_aspect) r.add(ti)
      endForEach
      return r

    method is_subclass( other:TypeInfo )->Logical
      #NOTE: This doesn't consider aspects
      if (this is other) return true
      if (other is null) return false
      if (base_class is null) return false
      return base_class.is_subclass(other)

    method find_property( property_name:String )->PropertyInfo
      forEach (p in properties)
        if (p.name == property_name) return p
      endForEach
      return null

    method find_global_method( name_or_sig:String, arg_count=null:Int32? )->MethodInfo
      return find_method( global_methods, name_or_sig, arg_count )

    method find_method( name_or_sig:String, arg_count=null:Int32? )->MethodInfo
      return find_method( methods, name_or_sig, arg_count )

    method find_method( method_list:MethodInfo[], name_or_sig:String, arg_count=null:Int32? )->MethodInfo
      if (name_or_sig.contains('('))
        forEach (m in method_list)
          if (m.signature == name_or_sig) return m
        endForEach
        return null
      endIf

      forEach (m in method_list)
        if (m.name == name_or_sig and (not arg_count.exists or m.parameter_count == arg_count.value))
          return m
        endIf
      endForEach

      return null

    method find_global_method( name_or_sig:String, args:Value )->MethodInfo
      return find_method( global_methods, name_or_sig, args )

    method find_method( name_or_sig:String, args:Value )->MethodInfo
      return find_method( methods, name_or_sig, args )

    method find_method( method_list:MethodInfo[], name_or_sig:String, args:Value )->MethodInfo
      if (name_or_sig.contains('('))
        forEach (m in method_list)
          if (m.signature == name_or_sig) return m
        endForEach
        return null
      endIf

      # Find first close match among overloads, falling back to last compatible match
      # (e.g. an integer arg is a close match with Int64 and Int32 and a real arg is
      # a compatible match with Int64 and Int32).
      local best_m : MethodInfo
      local fallback_m_matching_param_count : MethodInfo
      local fallback_m_matching_name        : MethodInfo

      forEach (m in method_list)
        contingent
          necessary (m.name == name_or_sig)
          fallback_m_matching_name = m
          necessary (m.parameter_count == args.count)
          fallback_m_matching_param_count = m

          forEach (arg at i in args)
            local p = m.parameter_type( i )

            if (p.is_primitive)
              if (p.name == "Logical")
                necessary (arg.is_logical)
              elseIf (p.name == "Character")
                necessary (arg.is_number or (arg.is_string and arg.count == 1))
              else
                necessary (arg.is_number)
              endIf

            elseIf (p.name == "String")
              necessary (arg.is_string)

            elseIf (p.is_reference)
              necessary (arg.is_object)
              local obj = arg->Object
              if (obj) necessary (obj.type_info.instance_of(p))

            endIf
          endForEach

        satisfied
          # This is a compatible method
          best_m = m

          # Now see if it's a close match by checking primitive types
          contingent
            forEach (arg at i in args)
              local p = m.parameter_type( i )

              if (arg.is_number)
                which (p.name)
                  case "Int64", "Int32", "Character", "Byte"
                    necessary (arg->Real64.fractional_part == 0)
                endWhich
              endIf

            endForEach

          satisfied
            # Close match - go ahead and return it
            return m

          endContingent

        endContingent
      endForEach

      if (not best_m) best_m = fallback_m_matching_param_count
      if (not best_m) best_m = fallback_m_matching_name

      if (best_m)
        return best_m
      else
        return null
      endIf

    method find_methods( method_name:String )->MethodInfo[] [macro]
      return this.method_info( method_name )

    method method_info( method_name:String )->MethodInfo[]
      local r = MethodInfo[]
      forEach (i in 0..<methods.count)
        if (methods[i].name == method_name)
          r.add( methods[ i ] )
        elseIf (methods[i].name.begins_with(method_name))
          local n = methods[i].name.before_first("<<").before_first("(")
          if (n == method_name)
            r.add( methods[ i ] )
          endIf
        endIf
      endForEach
      return r

    method property( context:Object, name:String )->Value
      return property( context, context.type_info.find_property(name) )

    method property( context:Object, property_info:PropertyInfo )->Value
      local type = property_info.type
      if (type.is_reference)
        return Value( property<<Object>>(context,property_info) )
      else
        which (type.primitive_type)
          case TYPE_REAL64
            return Value( property<<Real64>>(context,property_info) )
          case TYPE_REAL32
            return Value( property<<Real32>>(context,property_info) )
          case TYPE_INT64
            return Value( property<<Int64>>(context,property_info) )
          case TYPE_INT32
            return Value( property<<Int32>>(context,property_info) )
          case TYPE_CHARACTER
            return Value( property<<Character>>(context,property_info) )
          case TYPE_BYTE
            return Value( property<<Byte>>(context,property_info) )
          case TYPE_LOGICAL
            return Value( property<<Logical>>(context,property_info) )
          others
            # Must be a compound
            return context.introspector[ property_info ]
        endWhich
      endIf

    method property<<$PropertyType>>( context:Object, property_name:String )->$PropertyType
      return property<<$PropertyType>>( context, find_property(property_name) )

    method property<<$PropertyType>>( context:Object, property_info:PropertyInfo )->$PropertyType
      if (not property_info)
        local default_value : $PropertyType
        return default_value
      endIf

      local type = context.type_info
      if (isReference($PropertyType))
        return native( "*((RogueObject**)(((RogueByte*)(intptr_t)$context) + Rogue_types[$type->index].property_offsets[$property_info->index]))" )->$PropertyType
      else
        return native( "*(($($PropertyType)*)(((RogueByte*)(intptr_t)$context) + Rogue_types[$type->index].property_offsets[$property_info->index]))" )->$PropertyType
      endIf

    method set_global_property( property_name:String, value:Value )->this
      return set_global_property( find_global_property(property_name), value )

    method set_global_property( property_info:PropertyInfo, value:Value )->this
      if (not property_info) return this

      local i = property_info.index

      if (property_info.type.is_reference)
        local v = value->Object
        native( "*((RogueObject**)(Rogue_types[$index].global_property_pointers[$i])) = $v;" )

      else
        which (property_info.type.primitive_type)
          case TypeInfo.TYPE_REAL64
            local v = value->Real64
            native( "*((RogueReal64*)(Rogue_types[$index].global_property_pointers[$i])) = $v;" )

          case TypeInfo.TYPE_REAL32
            local v = value->Real32
            native( "*((RogueReal32*)(Rogue_types[$index].global_property_pointers[$i])) = $v;" )

          case TypeInfo.TYPE_INT64
            local v = value->Int64
            native( "*((RogueInt64*)(Rogue_types[$index].global_property_pointers[$i])) = $v;" )

          case TypeInfo.TYPE_INT32
            local v = value->Int32
            native( "*((RogueInt32*)(Rogue_types[$index].global_property_pointers[$i])) = $v;" )

          case TypeInfo.TYPE_CHARACTER
            local v = value->Character
            native( "*((RogueCharacter*)(Rogue_types[$index].global_property_pointers[$i])) = $v;" )

          case TypeInfo.TYPE_BYTE
            local v = value->Byte
            native( "*((RogueByte*)(Rogue_types[$index].global_property_pointers[$i])) = $v;" )

          case TypeInfo.TYPE_LOGICAL
            local v = value->Logical
            native( "*((RogueLogical*)(Rogue_types[$index].global_property_pointers[$i])) = $v;" )

          others
            # Must be a compound
            Introspector( null, native("(RogueInt64)(intptr_t)(Rogue_types[$index].global_property_pointers[$i])")->Int64,
            property_info.type ).set( value )
        endWhich

      endIf

      return this

    method set_global_property<<$PropertyType>>( property_name:String, new_value:$PropertyType )->this
      return set_global_property<<$PropertyType>>( find_global_property(property_name), new_value )

    method set_global_property<<$PropertyType>>( property_info:PropertyInfo, new_value:$PropertyType )->this
      if (not property_info) return this

      local i = property_info.index
      if (isReference($PropertyType))
        native( "*((RogueObject**)(Rogue_types[$index].global_property_pointers[$i])) = $new_value;" )
      else
        native( "*(($($PropertyType)*)(Rogue_types[$index].global_property_pointers[$i])) = $new_value;" )
      endIf

      return this

    method set_property( context:Object, name:String, new_value:Value )->this
      return set_property( context, context.type_info.find_property(name), new_value )

    method set_property( context:Object, property_info:PropertyInfo, new_value:Value )->this
      local type = property_info.type
      if (type.is_reference)
        if (new_value.is_object or new_value.is_string)
          return set_property<<Object>>( context, property_info, new_value->Object )  # unpack value
        else
          return set_property<<Object>>( context, property_info, new_value )  # leave value wrapper
        endIf
      else
        which (type.primitive_type)
          case TYPE_REAL64
            return set_property<<Real64>>( context, property_info, new_value->Real64 )
          case TYPE_REAL32
            return set_property<<Real32>>( context, property_info, new_value->Real32 )
          case TYPE_INT64
            return set_property<<Int64>>( context, property_info, new_value->Int64 )
          case TYPE_INT32
            return set_property<<Int32>>( context, property_info, new_value->Int32 )
          case TYPE_CHARACTER
            return set_property<<Character>>( context, property_info, new_value->Character )
          case TYPE_BYTE
            return set_property<<Byte>>( context, property_info, new_value->Byte )
          case TYPE_LOGICAL
            return set_property<<Logical>>( context, property_info, new_value->Logical )
          others
            # Must be a compound
            context.introspector[ property_info ] = new_value
            return this
        endWhich
      endIf

    method set_property<<$PropertyType>>( context:Object, property_name:String, new_value:$PropertyType )->this
      return set_property<<$PropertyType>>( context, find_property(property_name), new_value )

    method set_property<<$PropertyType>>( context:Object, property_info:PropertyInfo, new_value:$PropertyType )->this
      if (not property_info) return this

      local type = context.type_info
      if (isReference($PropertyType))
        native( "*((RogueObject**)(((RogueByte*)(intptr_t)$context) + Rogue_types[$type->index].property_offsets[$property_info->index])) = $new_value;" )
      else
        native( "*(($($PropertyType)*)(((RogueByte*)(intptr_t)$context) + Rogue_types[$type->index].property_offsets[$property_info->index])) = $new_value;" )
      endIf

      return this

    method set_singleton( new_singleton:Object )->this
      if ((index < 0) or (index >= type_count)) return this
      native @|Rogue_types[$index]._singleton = $new_singleton;
      return this

    method singleton->Object
      if ((index < 0) or (index >= type_count)) return null
      return native("RogueType_singleton(&Rogue_types[$index])")->Object

    method singleton<<$OfType>>->$OfType
      return singleton as $OfType

    method to->String
      return name
endClass


class RuntimeTypeInfoLookup : StringTable<<TypeInfo>> [singleton];


class PropertyInfo
  PROPERTIES
    index               : Int32
    property_name_index : Int32
    property_type_index : Int32

  METHODS
    method init( index, property_name_index, property_type_index )

    method name->String
      return Runtime.literal_string( property_name_index )

    method type->TypeInfo
      return TypeInfo.get( property_type_index )

    method to->String
      return "$:$" (name,type.name)
endClass


class MethodInfo
  GLOBAL PROPERTIES
    _method_name_strings = Table<<Int32,String>>()

  GLOBAL METHODS
    method _get_method_name ( method_index : Int32 ) -> String
      local name = _method_name_strings[method_index]
      if (not name)
        native @|int name_index = Rogue_method_info_table[Rogue_method_info_index[$method_index]][0];
                |if (name_index >= 0) $name = RogueString_create_from_utf8(Rogue_method_name_strings[name_index]);
        require(name)
        _method_name_strings[method_index] = name
      endIf
      return name

  PROPERTIES
    index               : Int32
    name                : String
    base_name           : String
    signature           : String
    fn_ptr              : IntPtr
    parameter_count     : Int32
    call_handler        : Int32
    _first_param_name   : Int32
    _first_param_type   : Int32
    _return_type        : Int32

  METHODS
    method init( index, parameter_count, _first_param_name, _first_param_type, _return_type, call_handler )
      fn_ptr = native("(intptr_t)Rogue_dynamic_method_table[$this->index]")->IntPtr

    method base_name->String
      if (@base_name) return @base_name
      @base_name = name.before_first('<')
      return @base_name

    method name->String
      if (@name) return @name
      @name = _get_method_name(index)
      return @name

    method return_type->TypeInfo
      if (_return_type == -1) return null
      return TypeInfo.get(_return_type)

    method parameter_name( index:Int32 )->String [essential]
      if (index < 0 or index >= parameter_count) return null
      return native( "Rogue_literal_strings[ Rogue_method_param_names[$_first_param_name + $index] ]" )->String

    method parameter_type( index:Int32 )->TypeInfo [essential]
      if (index < 0 or index >= parameter_count) return null
      return TypeInfo.get( native("Rogue_method_param_types[$_first_param_type + $index]")->Int32 )

    method signature->String
      if (@signature) return @signature
      local buffer = StringBuilder().print( name ).print( '(' )
      forEach (i in 0..<parameter_count)
        if (i > 0) buffer.print( ',' )
        buffer.print( parameter_type(i).name )
      endForEach
      buffer.print( ')' )
      signature = buffer->String
      return @signature

    method to->String
      if (return_type) return signature + "->" + return_type.name
      return signature

    method call( args:Value )->Value
      if (args is null) args = UndefinedValue
      return call( native("$this->fn_ptr")->IntPtr, call_handler, args )

    method call( context:Object, args:Value )->Value
      if (args is not null and not args.is_null)
        args.insert( Value(context) )
        local result = call( args )
        args.remove_first
        return result
      else
        return call( @[ Value(context) ] )
      endIf

    # Class methods
    method call ()
      _check_noreturn()
      _check_count(0)
      native "((void (*)())($this->fn_ptr))();"

    method call<<$T1>> (t1:$T1)
      _check_noreturn()
      _check_count(1)
      native "((void (*)($(t1.type)))($this->fn_ptr))($t1);"

    method call<<$T1,$T2>> (t1:$T1,t2:$T2)
      _check_noreturn()
      _check_count(2)
      native "((void (*)($(t1.type),$(t2.type)))($this->fn_ptr))($t1,$t2);"

    method call<<$T1,$T2,$T3>> (t1:$T1,t2:$T2,t3:$T3)
      _check_noreturn()
      _check_count(3)
      native "((void (*)($(t1.type),$(t2.type),$(t3.type)))($this->fn_ptr))($t1,$t2,$t3);"

    method call<<$T1,$T2,$T3,$T4>> (t1:$T1,t2:$T2,t3:$T3,t4:$T4)
      _check_noreturn()
      _check_count(4)
      native "((void (*)($(t1.type),$(t2.type),$(t3.type),$(t4.type)))($this->fn_ptr))($t1,$t2,$t3,$t4);"

    method call<<$T1,$T2,$T3,$T4,$T5>> (t1:$T1,t2:$T2,t3:$T3,t4:$T4,t5:$T5)
      _check_noreturn()
      _check_count(5)
      native "((void (*)($(t1.type),$(t2.type),$(t3.type),$(t4.type),$(t5.type)))($this->fn_ptr))($t1,$t2,$t3,$t4,$t5);"

    # Object methods
    method call (o:Object)
      _check_noreturn()
      _check_count(0)
      native "((void (*)(RogueObject*))($this->fn_ptr))($o);"

    method call<<$T1>> (o:Object,t1:$T1)
      _check_noreturn()
      _check_count(1)
      native "((void (*)(RogueObject*,$(t1.type)))($this->fn_ptr))($o,$t1);"

    method call<<$T1,$T2>> (o:Object,t1:$T1,t2:$T2)
      _check_noreturn()
      _check_count(2)
      native "((void (*)(RogueObject*,$(t1.type),$(t2.type)))($this->fn_ptr))($o,$t1,$t2);"

    method call<<$T1,$T2,$T3>> (o:Object,t1:$T1,t2:$T2,t3:$T3)
      _check_noreturn()
      _check_count(3)
      native "((void (*)(RogueObject*,$(t1.type),$(t2.type),$(t3.type)))($this->fn_ptr))($o,$t1,$t2,$t3);"

    method call<<$T1,$T2,$T3,$T4>> (o:Object,t1:$T1,t2:$T2,t3:$T3,t4:$T4)
      _check_noreturn()
      _check_count(4)
      native "((void (*)(RogueObject*,$(t1.type),$(t2.type),$(t3.type),$(t4.type)))($this->fn_ptr))($o,$t1,$t2,$t3,$t4);"

    method call<<$T1,$T2,$T3,$T4,$T5>> (o:Object,t1:$T1,t2:$T2,t3:$T3,t4:$T4,t5:$T5)
      _check_noreturn()
      _check_count(5)
      native "((void (*)(RogueObject*,$(t1.type),$(t2.type),$(t3.type),$(t4.type),$(t5.type)))($this->fn_ptr))($o,$t1,$t2,$t3,$t4,$t5);"

    method returns<<$RT>> () -> MethodCallerReturning<<$RT>>
      _check_return<<$RT>>()
      return MethodCallerReturning<<$RT>>(this)

  METHODS  # helper methods
    method _check_count (c : Int32)
      if (c != parameter_count) throw Error("$ arguments passed to method taking $." (c, "# argument".pluralized(parameter_count)))

    method _check_return<<$RT>> ()
      if (_return_type == -1)
        throw Error("Method does not return a value; Do not specify one using '->' or 'returns<<...>>'.")
      else
        if (isReference($RT))
          if (not return_type.is_subclass(<<$RT>>))
            throw Error("Wrong return type specified.  Use .call(...)->$." (return_type.name))
          endIf
        else
          if (<<$RT>> is not return_type)
            throw Error("Wrong return type specified.  Use .call(...)->$." (return_type.name))
          endIf
        endIf
      endIf

    method _check_noreturn ()
      if (_return_type != -1)
        throw Error("Explicit return type required.  Use .call(...)->$." (return_type.name))
      endIf



  GLOBAL METHODS
    method call( fn:IntPtr, i:Int32, args:Value )->Value
      # CAUTION: leave parameter names as they are - compiler relies on these specific parameter names
      #
      # fn: function pointer
      # i:  IntrospectionCallHandler index
      # Send a @[list] as the value args.

      # <Compiler will insert generated code here>

      return UndefinedValue
endClass

class DynamicCallback [aspect]
  METHODS
    method update_function_pointer
      local m = DynamicCallback.find_compatible_method( context.type_info, original_signature, specializing_signature )
      if (m?)
        local new_ptr = m.fn_ptr
        native @|THIS->fn_ptr = (void*) $new_ptr;
        return
      endIf

  GLOBAL METHODS
    method find_compatible_method( context_type:TypeInfo, original_signature:String, specializing_signature:String )->MethodInfo
      local m = context_type.find_method( specializing_signature )
      if (m) return m

      local original_i1 = find_opening_paren( specializing_signature )
      if (not original_i1.exists) return null
      local specialized_i1 = find_opening_paren( specializing_signature )
      if (not specialized_i1.exists) return null

      use builder=StringBuilder.pool
        local original_param_types = TypeInfo[]
        local specialized_param_types = TypeInfo[]

        DynamicCallback.parse_param_types( original_signature, original_i1.value+1, original_param_types, builder )
        DynamicCallback.parse_param_types( specializing_signature, specialized_i1.value+1, specialized_param_types, builder )

        if (original_param_types.count != specialized_param_types.count) return null

        local base_name = specializing_signature.leftmost( specialized_i1.value )

        if ((forEach in original_param_types) is null) return null
        if ((forEach in specialized_param_types) is null) return null

        forEach (original_type at i in original_param_types)
          local specialized_type = specialized_param_types[i]

          # Step specialized param types down one by one until they match the original type,
          # checking each new variation.
          local base_class = specialized_type.base_class
          while (base_class and base_class.instance_of(original_type))
            specialized_param_types[i] = base_class
            builder.clear.print( base_name ).print( '(' )
            forEach (cur_type at j in specialized_param_types)
              if (j > 0) builder.print(',')
              builder.print( cur_type )
            endForEach
            builder.print( ')' )

            m = context_type.find_method( builder->String )
            if (m) return m

            base_class = base_class.base_class
          endWhile
        endForEach
      endUse

      return null

    method find_opening_paren( signature:String )->Int32?
      # Find the first open paren. Need to skip past method template specializers if present.
      local depth =  0
      forEach (ch at i in signature)
        which (ch)
          case '('
            ++depth
            if (depth == 1) return i
          case '{', '[', '<'
            ++depth
          case ')', '}', ']', '>'
            --depth
        endWhich
      endForEach
      return null

    method parse_param_types( signature:String, i1:Int32, param_types:TypeInfo[], builder:StringBuilder )
      builder.clear
      local depth = 1
      forEach (ch at i in signature from i1)
        which (ch)
          case '(', '{', '[', '<'
            ++depth
            builder.print( ch )
          case '}', ']', '>'
            --depth
            builder.print( ch )
          case ')'
            --depth
            if (depth == 0)
              # end of param types
              param_types.add( TypeInfo[builder] )
              escapeForEach
            else
              builder.print( ')' )
            endIf
          case ','
            if (depth == 1)
              param_types.add( TypeInfo[builder] )
              builder.clear
            else
              builder.print( ',' )
            endIf
          others
            builder.print( ch )
        endWhich
      endForEach
endClass

class NoSuchMethodError : Error
  METHODS
    method init( sig:String )
      prior.init( "No such method for introspection call: $." (sig) )
endClass

class InstanceError : Error
endClass

class MethodCallerReturning <<$R>>
  PROPERTIES
    fn_ptr : IntPtr
    parent : MethodInfo

  METHODS
    method init (parent)
      fn_ptr = parent.fn_ptr

  METHODS  # Global Callers
    method call () -> $R
      parent._check_count(0)
      local r : $R
      native "$r = (($(r.type) (*)())($this->fn_ptr))();"
      return r

    method call<<$T1>> (t1:$T1) -> $R
      parent._check_count(1)
      local r : $R
      native "$r = (($(r.type) (*)($(t1.type)))($this->fn_ptr))($t1);"
      return r

    method call<<$T1,$T2>> (t1:$T1,t2:$T2) -> $R
      parent._check_count(2)
      local r : $R
      native "$r = (($(r.type) (*)($(t1.type),$(t2.type)))($this->fn_ptr))($t1,$t2);"
      return r

    method call<<$T1,$T2,$T3>> (t1:$T1,t2:$T2,t3:$T3) -> $R
      parent._check_count(3)
      local r : $R
      native "$r = (($(r.type) (*)($(t1.type),$(t2.type),$(t3.type)))($this->fn_ptr))($t1,$t2,$t3);"
      return r

    method call<<$T1,$T2,$T3,$T4>> (t1:$T1,t2:$T2,t3:$T3,t4:$T4) -> $R
      parent._check_count(4)
      local r : $R
      native "$r = (($(r.type) (*)($(t1.type),$(t2.type),$(t3.type),$(t4.type)))($this->fn_ptr))($t1,$t2,$t3,$t4);"
      return r

    method call<<$T1,$T2,$T3,$T4,$T5>> (t1:$T1,t2:$T2,t3:$T3,t4:$T4,t5:$T5) -> $R
      parent._check_count(5)
      local r : $R
      native "$r = (($(r.type) (*)($(t1.type),$(t2.type),$(t3.type),$(t4.type),$(t5.type)))($this->fn_ptr))($t1,$t2,$t3,$t4,$t5);"
      return r

  METHODS  # Instance Callers
    method call (o:Object) -> $R
      parent._check_count(0)
      local r : $R
      native "$r = (($(r.type) (*)(RogueObject*))($this->fn_ptr))($o);"
      return r

    method call<<$T1>> (o:Object,t1:$T1) -> $R
      parent._check_count(1)
      local r : $R
      native "$r = (($(r.type) (*)(RogueObject*,$(t1.type)))($this->fn_ptr))($o,$t1);"
      return r

    method call<<$T1,$T2>> (o:Object,t1:$T1,t2:$T2) -> $R
      parent._check_count(2)
      local r : $R
      native "$r = (($(r.type) (*)(RogueObject*,$(t1.type),$(t2.type)))($this->fn_ptr))($o,$t1,$t2);"
      return r

    method call<<$T1,$T2,$T3>> (o:Object,t1:$T1,t2:$T2,t3:$T3) -> $R
      parent._check_count(3)
      local r : $R
      native "$r = (($(r.type) (*)(RogueObject*,$(t1.type),$(t2.type),$(t3.type)))($this->fn_ptr))($o,$t1,$t2,$t3);"
      return r

    method call<<$T1,$T2,$T3,$T4>> (o:Object,t1:$T1,t2:$T2,t3:$T3,t4:$T4) -> $R
      parent._check_count(4)
      local r : $R
      native "$r = (($(r.type) (*)(RogueObject*,$(t1.type),$(t2.type),$(t3.type),$(t4.type)))($this->fn_ptr))($o,$t1,$t2,$t3,$t4);"
      return r

    method call<<$T1,$T2,$T3,$T4,$T5>> (o:Object,t1:$T1,t2:$T2,t3:$T3,t4:$T4,t5:$T5) -> $R
      parent._check_count(5)
      local r : $R
      native "$r = (($(r.type) (*)(RogueObject*,$(t1.type),$(t2.type),$(t3.type),$(t4.type),$(t5.type)))($this->fn_ptr))($o,$t1,$t2,$t3,$t4,$t5);"
      return r
endClass
