$include Control

class State<<$ExtendedType>> : State<<$ExtendedType,change_state,exit_state>>
endClass

class State<<$ExtendedType,$change_state,$exit_state>>
  DEFINITIONS
    # Duration values
    INSTANT    =  0  # one and done
    INDEFINITE = -1  # action loops until finish() called or overridden is_finished()->true [default]

  GLOBAL METHODS
    method create( fn:Function() )->$ExtendedType
      return StateCall<<$ExtendedType>>( fn )

    method create( fn:Function(Real), duration:Real )->$ExtendedType
      return StateCallWithProgress<<$ExtendedType>>( fn, duration )

    method operator+( a:$ExtendedType, b:$ExtendedType )->$ExtendedType
      if (not a) return b
      if (not b) return a
      return a.and_then( b )

    method operator|( a:$ExtendedType, b:$ExtendedType )->$ExtendedType
      if (not a) return b
      if (not b) return a
      return a.also( b )

    method wait( duration:Real )->$ExtendedType
      return StateWait<<$ExtendedType>>( duration )

    method wait_for( condition:Function->Logical, duration=$ExtendedType.INDEFINITE:Real )->$ExtendedType
      return StateWaitFor<<$ExtendedType>>( condition, duration )

    method yield->$ExtendedType
      return StateYield<<$ExtendedType>>()

  PROPERTIES
    duration    : Real

    next_state       : $ExtendedType
    concurrent_state : $ExtendedType

    t           : Real
    dt          : Real  # time since last update
    prev_time   : Real  # 0 = not started

    is_exited   : Logical # flag that assists when states finish outside of updated()

  METHODS
    method init
      noAction

    method init( duration )

    method and_then( state:$ExtendedType )->$ExtendedType
      if (state)
        local cur = this
        while (cur.next_state) cur = cur.next_state
        cur.next_state = state
      endIf
      return this->(as $ExtendedType)

    method and_then_also( state:$ExtendedType )->$ExtendedType
      if (next_state) next_state = next_state.also( state )
      else      next_state = state
      return this->(as $ExtendedType)

    method also( state:$ExtendedType )->$ExtendedType
      if (state)
        local cur = this
        while (cur.concurrent_state) cur = cur.concurrent_state
        cur.concurrent_state = state
      endIf
      return this->(as $ExtendedType)

    method call<<$method_name>>()
      # Calls specified method on this state and all concurrent states.
      this->(as $ExtendedType).$method_name()
      local cur = concurrent_state
      while (cur)
        cur.$method_name()
        cur .= concurrent_state
      endWhile

    method call<<$method_name,$ArgType>>( arg:$ArgType )
      # Calls specified method on this state and all concurrent states.
      this->(as $ExtendedType).$method_name( arg )
      local cur = concurrent_state
      while (cur)
        cur.$method_name( arg )
        cur .= concurrent_state
      endWhile

    method $change_state( new_state:$ExtendedType )
      next_state = new_state
      $exit_state

    method count->Int32
      local n = 1
      if (next_state) n += next_state.count
      if (concurrent_state) n += concurrent_state.count
      return n

    method describe->String
      return type_name

    method description->String
      local result = describe
      if (next_state) result = "$ + $" (result,next_state->String)
      if (concurrent_state) result = "($) | ($)" (result,concurrent_state)
      return result

    method $exit_state
      is_exited = true
      finish

    method find_active<<$FindType>>()->$ExtendedType
      # Returns any active state (this or a concurrent_state) that is
      # instanceOf $FindType.
      local cur = this->(as $ExtendedType)
      while (cur)
        if (cur instanceOf $FindType) return cur
        cur .= concurrent_state
      endWhile
      return null

    method finish
      if (duration < 0) duration = 0
      t = duration

    method insert_next( state:$ExtendedType )
      if (state) next_state = state.and_then( next_state )

    method is_finished->Logical
      return (duration >= 0 and t >= duration)

    method last->$ExtendedType
      # Returns the last state in this chain
      local cur = this
      while (cur.next_state ) cur = cur.next_state
      return cur

    method on_execute
      # Override to automatically make duration INSTANT
      duration = INDEFINITE  # only happens when not overridden
      on_start  # start an indefinite state

    method on_start

    method on_update

    method on_finish

    method progress->Real
      return which{ duration>0:t/duration || 1.0 }

    method started->$ExtendedType
      if (is_exited)
        # Already exited outside of updated()
        if (concurrent_state)
          concurrent_state = concurrent_state.started
        endIf
      else
        if (not prev_time)
          local cur_time = System.execution_time
          if (duration)
            on_start
          else
            on_execute
            # Default will change duration to INDEFINITE and call on_start()
            # Override on_execute() to keep INSTANT.
          endIf
          prev_time = cur_time
        endIf

        if (concurrent_state) concurrent_state = concurrent_state.started
        if (not is_finished) return this->(as $ExtendedType)
      endIf

      .handle_finish

      return .start_next

    method to->String
      return description

    method update
      on_update

    method updated->$ExtendedType
      if (is_exited)
        # Already exited outside of updated()
        if (concurrent_state)
          concurrent_state = concurrent_state.updated
        endIf
      else
        local cur_time = System.execution_time
        if (not prev_time)
          dt = 1.0/60.0
          if (duration)
            on_start
          else
            on_execute
            # Default will change duration to INDEFINITE and call on_start()
            # Override on_execute() to keep INSTANT.
          endIf
        else
          dt = (cur_time - prev_time)
          if (dt > 1.0/10.0) dt = 1.0/60.0
          t += dt
          if (duration >= 0 and t > duration) t = duration
        endIf

        prev_time = cur_time

        update

        if (concurrent_state)
          concurrent_state = concurrent_state.updated
        endIf

        if (not is_finished) return this->(as $ExtendedType)
      endIf

      .handle_finish

      return .update_next

    method .handle_finish
      on_finish

    method .start_next->$ExtendedType
      # Finds the successor to this state, adjusts links if necessary, and
      # returns it or the result of a call to its started().
      if (next_state)
        if (concurrent_state)
          # Attach any parallel commands remaining in this finished command to the
          # next_state command in this chain that will be the new head.
          next_state = next_state.also( concurrent_state )
          concurrent_state = null
        endIf
        return next_state.started
      elseIf (concurrent_state)
        # .start_next() is called after updated(), which has already started 'concurrent_state'.
        return concurrent_state
      else
        return null
      endIf

    method .update_next->$ExtendedType
      # Finds the successor to this state, adjusts links if necessary, and
      # returns it or the result of a call to its updated().
      if (next_state)
        if (concurrent_state)
          # Attach any parallel commands remaining in this finished command to the
          # next_state command in this chain that will be the new head.
          next_state = next_state.also( concurrent_state )
          concurrent_state = null
        endIf
        return next_state.updated
      elseIf (concurrent_state)
        # .update_next() is called after updated(), which has already updated 'concurrent_state'.
        return concurrent_state
      else
        return null
      endIf

endClass

class StateWait<<$ExtendedType>>( duration ) : $ExtendedType [undocumented];

class StateWaitFor<<$ExtendedType>>( condition:Function->Logical, duration=$ExtendedType.INDEFINITE )
  : $ExtendedType [undocumented]
  METHODS
    method is_finished->Logical
      return condition()
endClass

class StateCall<<$ExtendedType>>( fn:Function(), duration=$ExtendedType.INSTANT ) : $ExtendedType [undocumented]
  METHODS
    method on_execute
      fn()
endClass

class StateCallWithProgress<<$ExtendedType>>( fn:Function(Real), duration ) : $ExtendedType [undocumented]
  METHODS
    method on_update
      fn( progress )
endClass

class StateYield<<$ExtendedType>> : $ExtendedType
  PROPERTIES
    update_count = 0

  METHODS
    method is_finished->Logical
      return update_count == 2

    method on_update
      ++update_count
endClass
