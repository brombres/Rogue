$define VERSION "2.38"
$define DATE    "April 22, 2025"

library RogueC

uses Codec/Base64

$include Macros

$define VCALLS true
# Enable vtable virtual calls where possible. If 'false' then dynamic dispatch used instead.

$include AccessAndAssignPatcher
$include Analytics
$include AspectMethodPatcher
$include AssignIDs
$include Attributes
$include BranchAnalyzer
$include BuildDynamicMethodTable
$include Candidates
$include ClearDynamicDispatchFlags
$include ClearLocalInfo
$include CollectVirtualCallInfo
$include Collector
$include ConvertDynamicCallsToStaticAndVirtualCalls
$include CullUnusedElements
$include ContextBlockPatcher
$include ControlType
$include CmdType
$include CGenerator
$include CSpecializer
$include CWriter
$include DynamicMethodGroup
$include ExceptionAnalyzer
$include ExceptionType
$include ExceptionTypes
$include Finalizer
$include ImplicitFunctionGenerator
$include ImplicitWithVarCollector
$include InspectionVisitor
$include Macro
$include Meta
$include Library
$include NeedsDecomposition
$include MethodOrganizer
$include Parser
$include Procedure
$include Program
$include PropertyOrganizer
$include Resolver
$include ResolveUsedElements
$include Scanner
$include ScopeVisitor
$include Template
$include TransformInlineForEach
$include Type
$include TypeOrganizer
$include EndScopePatcher

uses Console/CommandLineParser
uses Utility/VersionNumber

try
  RogueC( System.command_line_arguments )

catch (error:CompileError)
  Console.error.println error
  System.exit 1
catch (error:Error)
  error.display
  System.exit 1
endTry

class RogueC
  PROPERTIES
    command : Variant

  METHODS
    method init( args:String[] )
      Analytics.total_time.start

      try
        command = parse_args( args )
        Program.command = command
      catch (err:Error)
        Console.error.println "=" * 80.or_smaller(Console.width)
        Console.error.println err
        Console.error.println "=" * 80.or_smaller(Console.width)
        println "Run 'roguec' without arguments to see usage."
        System.exit 1
      endTry

      if (command//options//version)
        print   "v"
        println VERSION
        println "$ by Brom Bresenham" (DATE)
        System.exit 0
      endIf

      if (command//options//help or (command//args.is_empty and not command//options//doc))
        print_usage
        System.exit 0
      endIf

      if (command//options//libraries)
        local paths = @[]
        forEach (lib in command//options//libraries)
          local path = lib->String.replacing(';',',')
          if (not System.is_windows) path .= replacing(':',',')
          paths.add( forEach in path->String.split(',') )
        endForEach
        command//options//libraries = paths
        Program.add_include_search_path( forEach in paths, &is_library )
      endIf

      if (command//options//int)
        local int_bits = command//options//int->Int
        if (int_bits != 32 and int_bits != 64)
          throw Error( "--int can only be set to 32 or 64." )
        endIf
        Program.int_bits = int_bits
      endIf

      if (command//options//real)
        local real_bits = command//options//real->Int
        if (real_bits != 32 and real_bits != 64)
          throw Error( "--real can only be set to 32 or 64." )
        endIf
        Program.real_bits = real_bits
      endIf

      if (command//options//namespace)
        local ns = command//options//namespace->String
        Program.namespace_lc = ns
        Program.namespace_capitalized = ns.capitalized
        Program.namespace_uc = ns.to_uppercase
        if (Program.namespace_uc.count) Program.namespace_uc .= with_suffix( '_' )
      endIf

      if local split = command//options//split
        if (split.is_string)
          Program.split_line_count = split->String.replacing(",","").replacing("_","")->Int
        else
          Program.split_line_count = 50_000
        endIf
      endIf

      local api_includes = command//options//api.contains(true)
      if (api_includes)
        Scanner.uses_Introspection = true
        Scanner.uses_autoinclude = true
      endIf

      local libraries_folder = File( File( System.executable_filepath ).folder )
      if ((libraries_folder/"Libraries").exists)
        libraries_folder /= "Libraries"
      else
        if ((libraries_folder/"Source/Libraries").exists)
          libraries_folder /= "Source/Libraries"
        else
          libraries_folder .= parent
          if ((libraries_folder/"Source/Libraries").exists)
            libraries_folder /= "Source/Libraries"
          elseIf ((libraries_folder/"Libraries").exists)
            libraries_folder /= "Libraries"
          else
            libraries_folder = File(".")
          endIf
        endIf
      endIf

      if (Program.no_stdlib)
        Program.include( forEach in command//args, &api=api_includes )
      else
        Program.add_include_search_path( libraries_folder, &is_library )
        Program.add_include_search_path( libraries_folder/"Standard", &is_library )
        Program.include( "StandardMacros" )
        Program.include( forEach in command//args, &api=api_includes )
        Program.include( "Standard" )
      endIf

      if (Program.generate_doc)
        ensure<<Program.doc_roots>>
        forEach (doc_root in command//options//doc)
          if (doc_root == Variant(true))
            # Include all Standard Library files to generate the docs from.
            Program.doc_roots.add( libraries_folder.abs.filepath + '/' )
            Program.include( "Standard" )
            Program.include( forEach in Files(libraries_folder,"**/*.rogue"), &api=api_includes )
          else
            local doc_path = File( doc_root )
            local doc_filepath = doc_path.abs.filepath
            if (doc_path.is_folder)
              doc_filepath += '/'
              Program.include( forEach in Files(doc_path,"**/*.rogue"), &api=api_includes )
            else
              Program.include( doc_path.filepath, &api=api_includes )
            endIf
            Program.doc_roots.add( doc_filepath )
          endIf
        endForEach
      endIf

      forEach (option in command//options//api)
        if (option == Variant(true)) nextIteration
        if (option->String.ends_with(".rogue"))
          Program.include( option->String, &api )
        else
          Program.api_patterns.add( option->String )
        endIf
      endForEach

      check_autoincludes

      Program.organize

      if (Program.generate_doc)
        Program.instantiate_doc_method_templates
        Program.generate_doc_json
        return
      endIf

      Program.resolve

      Analytics.generate_c.start
      Program.generate_c
      Analytics.generate_c.stop
      Analytics.total_time.stop

      #Analytics.display
      #if ($defined(DEBUG)) Analytics.display

    method check_autoincludes
      while (Scanner.uses_autoinclude)
        local t = Program.t
        Scanner.uses_autoinclude = false
        if (Scanner.uses_DateTime) Program.include( t, "DateTime" ); Scanner.uses_DateTime = false
        if (Scanner.uses_File)     Program.include( t, "FileIO" ); Scanner.uses_File = false
        if (Scanner.uses_Introspection) Program.include( t, "Introspection" ); Scanner.uses_Introspection = false
        if (Scanner.uses_Process)  Program.include( t, "Core/Process" ); Scanner.uses_Process = false
        if (Scanner.uses_Promise)  Program.include( t, "Core/Promise" ); Scanner.uses_Promise = false
        if (Scanner.uses_Scanner)  Program.include( t, "IO/Scanner" ); Scanner.uses_Scanner = false
        if (Scanner.uses_Set)      Program.include( t, "Collection/Set" ); Scanner.uses_Set = false
        if (Scanner.uses_Table)    Program.include( t, "Collection/Table" ); Scanner.uses_Table = false
        if (Scanner.uses_Variant)    Program.include( t, "Core/Variant" ); Scanner.uses_Variant = false
      endWhile

    method parse_args( args:String[] )->Variant
      local command = CommandLineParser().
      [
        option( "--api=", &optional, &multi )
        option( "--debug" )
        option( "--define=", &alias="-D", &multi )
        option( "--doc=", &optional, &multi )
        option( "--gc=" )
        option( "--gc-threshold=" )
        option( "--help",       &aliases=["-h","-?"] )
        option( "--int=", &alias="-i" )
        option( "--libraries=", &alias="-L", &multi )
        option( "--main" )
        option( "--no-stdlib" )
        option( "--namespace=", &alias="-n" )
        option( "--output=" )
        option( "--real=", &alias="-r" )
        option( "--split=", &alias="-s", &optional )
        option( "--target=", &alias="-t", &multi )
        option( "--version", &alias="-v" )
      ].parse( args )

      local debug_t : Token
      if (command//options.contains("debug"))
        Program.debug_mode  = true
        Program.stack_trace = true
        debug_t = Program.t.cloned( TokenType.KEYWORD_TRUE )
      else
        debug_t = Program.t.cloned( TokenType.KEYWORD_FALSE )
      endIf
      Program.definitions["DEBUG"] = Token[].[add(debug_t)]

      if (command//options.contains("define"))
        forEach (def in command//options//define)
          block def = def->String
            local key   = def.before_first(':')
            local value = def.after_first(':')
            if (value == "") value = "true"
            local tokens = Scanner( "Command Line Option", value ).tokenize
            tokens.remove_last  # EOL
            Program.definitions[key] = tokens
          endBlock
        endForEach
      endIf

      if (command//options.contains("gc"))
        which (command//options//gc->String)
          case "auto":   Program.gc_auto = true
          case "manual": Program.gc_auto = false
          others
            throw Program.t.error( "Valid --gc modes are 'auto' and 'manual'." )
        endWhich
      endIf

      if (command//options.contains("gc-threshold"))
        local option = command//options["gc-threshold"]->String
        local value = option->Int
        if (option.ends_with("m",&ignore_case) or option.ends_with("mb",&ignore_case))
          value *= 1024*1024
        elseIf (option.ends_with("k",&ignore_case) or option.ends_with("kb",&ignore_case))
          value *= 1024
        endIf
        Program.gc_threshold = value
      endIf

      if (command//options//main)
        Program.generate_main = true
      endIf

      if (command//options//doc)
        Program.generate_doc = true
      endIf

      if (command//options.contains("no-stdlib"))
        Program.no_stdlib = true
      endIf

      if (command//options//target)
        forEach (target_option in command//options//target)
          local targets = target_option->String.replacing(';',',')
          if (not System.is_windows) targets .= replacing(':',',')
          forEach (target in targets.split(',') )
            if (target.begins_with('"') or target.begins_with('\''))
              target = target.leftmost(-1).rightmost(-1)
            endIf
            Program.targets[target] = target
            which (target)
              case "C++": Program.targets["CPP"] = "CPP"; Program.targets_cpp = true
              case "CPP": Program.targets["C++"] = "C++"; Program.targets_cpp = true
              case "Windows": Program.targets_windows = true
            endWhich
          endForEach
        endForEach
      endIf

      if (not Program.targets.count) Program.targets//C = "C"

      if (command//args.count > 0)
        if (command//options//output)
            local filepath = command//options//output->String
            if (File(filepath).is_folder)
              filepath /= command//args.first->String.before_suffix( ".rogue" )
            else
              local parent = File( filepath ).folder
              if (String.exists(parent)) File( parent ).create_folder
            endIf
            Program.output_filepath = filepath
        else
          Program.output_filepath = command//args.first->String.before_suffix( ".rogue" )
        endIf
      endIf

      return command

    method print_usage
      print   "RogueC v"
      println VERSION
      println "$ by Brom Bresenham" (DATE)
      println
      println @|USAGE
               |  rogue [OPTIONS] <filepath>
               |
               |OPTIONS
               |  --api, --api=Library[::ClassName], --api="Filename.rogue"
               |    --api
               |      All routines and classes in the files specified on the command line are
               |      marked [essential api], ensuring those routines, classes, and methods are
               |      available to introspection.
               |    --api=Library, --api="Library::ClassName", --api="*"
               |      Marks the specified class or library classes as [essential api].
               |    --api="Filename.rogue"
               |      The specified file is included and its routines and classes are marked
               |      [essential api].
               |
               |  --debug
               |    Compile in debug mode:
               |    - 'DEBUG' is defined as 'true' instead of 'false'.
               |    - Call history is tracked, which allows for stack frame display at the cost
               |      of additional overhead.
               |
               |  --define="name[:value]", -D "name[:value]"
               |    Adds a single preprocessor define.
               |    Defining "name:value" is equivalent to: $define name value
               |    Defining "name" is equivalent to:       $define name true
               |
               |  --doc
               |  --doc=<folder-path>
               |    RogueC generates documentation metadata instead of generating code.
               |    '--doc' generates documentation for the standard library.
               |    '--doc=<folder-path>' recursively includes and generates documentation for
               |    all .rogue files nested in the specified folder. The generated metadata is
               |    saved as a '.json' file.
               |
               |  --help, -h, -?
               |    Show this help text.
               |
               |  --gc=[auto|manual]
               |    Sets garbage collection mode:
               |      --gc=auto
               |        Collects after every [--gc-threshold] number of bytes have been
               |        allocated (default 1 MB). This is slower than 'manual' due to the extra
               |        overhead of tracking local variables.
               |      --gc=manual
               |        Rogue_check_gc() should be called by the C framework at a regular
               |        interval (such as after every frame) outside of any calls into Rogue
               |        code; this will call Rogue_collect_garbage() if the --gc-threshold has
               |        been reached. Or: never call GC for programs that complete a task and
               |        then exit; this will allow them to execute as fast as possible.
               |
               |  --gc-threshold={number}[MB|K]
               |    Sets the GC threshold in bytes (default), megabytes, or kilobytes.
               |    '1048576', '1024K', and '1MB' are all equivalent. The default is 1MB.
               |
               |  --int=32, -i 32
               |    Specifies that the 'Int' datatype should be 32 bits instead of 64.
               |    This affects the type of integer literals (0, 1, 0xff, ...) as well.
               |
               |  --libraries="path1;path2", -L path1:path2
               |    Specify paths RogueC should look through for '$include' and 'uses'
               |    directives. This option can be listed multiple times. Both ';' and ':' can
               |    be used as a path separator on quoted paths.
               |
               |  --main
               |    Generates a main() function with the C output.
               |
               |  --namespace=<TypePrefix>, -n <TypePrefix>
               |    Generates C type names with the specified prefix, allowing multiple,
               |    independent Rogue-generated C source files to be compiled together.
               |
               |  --no-stdlib
               |    Prevents the Standard Library from automatically being included. The usual
               |    include order is: StandardMacros, <command-line-files>, Standard.
               |
               |  --output=<filepath>
               |    Specify the destination folder and/or the base filename for generated C
               |    output.
               |
               |  --real=32, -r 32
               |    Specifies that the 'Real' datatype should be 32 bits instead of 64.
               |    This affects the type of floating point literals (2.54, pi) as well.
               |
               |  --split, --split=<line-count>, -s <line-count>
               |    Splits the generated .c/.cpp/.m/.mm into multiple files, each with
               |    approximately <line-count> lines. If '--split' is specified, the default
               |    line-count is 50,000. Monolithic source files 100k+ lines take
               |    exponentially longer to compile with optimizations than multiple smaller
               |    files. The output filenames are 'filename-1', 'filename-2', etc.
               |
               |  --target=Target1,"Target2",..., -t Target
               |    Specify arbitrary target languages, platforms, operating systems,
               |    frameworks, etc. that can be checked during compile time with '$target("C")'
               |    etc. This option can be listed multiple times. If no targets are given, a
               |    default target of "C" is used.
               |
               |  --version, -v
               |    Prints the Rogue version number and release date.

endClass

