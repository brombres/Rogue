module RogueC

class TransformInlineForEach : Visitor
  PROPERTIES
    inline_stack       = Cmd[]
    autoname_index     = 0
    current_statements : ListRewriter<<Cmd>>

  METHODS
    method autoname( base_name:String )->String
      local result = "_auto_$_$"(base_name,autoname_index)
      ++autoname_index
      return result

    method on_visit( cmd:InlineForEach )->Cmd
      local name = autoname( "iterator" )
      cmd.control.name = name
      visit( cmd.control )
      inline_stack.add( ForEach(cmd.t,cmd.control,null) )
      return Access( cmd.t, name )

    method on_visit_node( cmd:Procedure )
      autoname_index = 0
      visit_children( cmd )

    method on_visit_children( cmd:Statements )
      local current_statements = cmd.list.rewriter
      forEach (element in current_statements)
        local cur_statement_index = current_statements.write_index
        local inline_stack_fp = inline_stack.count

        local visited_element = visit( element )
        if (visited_element)
          if (visited_element instanceOf Statements)
            current_statements.write( forEach in visited_element )
          else
            current_statements.write( visited_element )
          endIf
        endIf

        # Turn inline commands inside out
        while (inline_stack.count > inline_stack_fp)
          local outer_cmd = inline_stack.remove_last

          local cmd_foreach = outer_cmd->(as ForEach)
          if (cmd_foreach)
            local statements = Statements( current_statements.list[cur_statement_index].t )
            forEach (i in cur_statement_index..<current_statements.write_index)
              statements.add( current_statements.list[i] )
              current_statements.list[i] = null
            endForEach

            current_statements.list[ cur_statement_index ] = outer_cmd
            current_statements.write_index = cur_statement_index + 1

            cmd_foreach.statements = statements
          else
            trace
            throw outer_cmd.t.error( "[INTERNAL] Unhandled inline command '$'."(outer_cmd.type_name) )
          endIf
        endWhile

      endForEach

endClass
